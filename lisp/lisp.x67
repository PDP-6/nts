TITLE LISP INTERPRETER

	;ACCUMULATOR USAGE
NIL=0	;ATOM HEAD FOR NIL
A=1	;ARG 1; VALUE; MARKED FROM
B=2	;ARG 2, MARKED FROM
C=3	;ARG 3; MARKED FROM
AR1=4	;ARG 4; MARKED FROM
AR2A=5	;ARG 5; MARKED FROM
NACS==5	;NUMBER OF AC MARKED FROM
T=6	;USED WITH LSUBR CALLS
TT=7	;TEMP
S=11
D=12	;NOT MARKED, USED  BY PRINT,GC, ETC.
R=13	;DITTO
P"=14	;PDL AC; NOT MARKED
F=15	;DITTO
FF=16
SP=17	;SPECIAL PDL AC

OBTSIZ==77


RELOCATABLE


;BSUP=740000		;;XXX actually doesn't exist
BSUP=0
MACGO=BSUP+37400
MACCR=BSUP+37777
MACSA=BSUP+37762
VID=754
PCHCHN"=3
APRCHN"=4
LPTCHN"=5
TTYCHN==6
DISCHN==7
FLGCHN==6
PRS==PI


JCALLF=(77000)
CALLF=(76000)

LERR=1_33	;ORDINARY LISP ERROR
LER2=2_33	;ERROR OP USED BY PICPAC
LER3=3_33	;EPRINT, THEN LERR
STRT=4_33	;STRING TYPEOUT

LISPGO":
LOC 41
	JSR UUOH

LOC 40+2*APRCHN"
	JSR APRINT

LOC 40+2*TTYCHN"
	JSR PITELE
	JSR RECYC

LOC 40+2*DISCHN"
	BLKO DIS,BLKOP
	CONO PI,4000+1_<7-FLGCHN>
LOC 70
REPEAT 6,.RPCNT,,.RPCNT		;COMMONLY USED CONSTANTS

LOC LISPGO
	CONO PRS,11377
	CONO 635550
	CONO TTY,10+TTYCHN
LOSP:	SETZB F,FF
	MOVE P,C2
LISP:	CONO PI,12200+1_<7-TTYCHN">+1_<7-APRCHN">+1_<7-PCHCHN">+1_<7-DISCHN">+1_<7-LPTCHN">
	CONO  3000+APRCHN
LSPRT1:	SETOM ERRSW
	SETZM ERRTN
	SETZM TAPRED
	SETZM TTYOFF
	MOVE P,C2
	MOVE SP,SC2
NILPTR:	HRROI NIL,CNIL2
LISP1:	PUSHJ P,TERPRI
	PUSHJ P,READ
PPEVAL:	PUSHJ P,EVAL
	PUSHJ P,PRINT
	JRST LISP1

C2:	0	;INITIAL SYSTEM PDL SETTING, SET BY ALLOC.
SC2:	0	;INITIAL SPECIAL PDL, DITTO.

LSPRET:	MOVE B,SC2
	SKIPN RSTSW
	PUSHJ P,UBD
	JRST LSPRT1

RSTSW:	0

.RSET:	MOVEM A,RSTSW
	POPJ P,

APRINT:	0
	CONSO,1000
	JRST APRIN1
	AOS TEMPUS
	JSR APRBRK"
	CONO,1000+APRCHN
	SKIPE VDISLIST
	AOSG DISTM'
	JRST 12,@APRINT
	CONO DIS,3100+FLGCHN_3
	DATAO DIS,.-1]
	JRST 12,@APRINT

APRIN1:	CONSO 200000
	LER3 @APRINT
APDLO:	JUMPN NIL,MES21
	AOSLE DIEF
	STRT [SIXBIT \PDL OV FROM GC--LISP IS DEAD!\]

MES21:	SETZB F,FF
	SETZM 40
	SKIPL P
	STRT [SIXBIT \REG !\]
	SKIPL SP
	STRT [SIXBIT \SPEC !\]
	SKIPE 40
	LER2 [SIXBIT \PUSHDOWN CAPACITY EXCEEDED!\]
	JRST 12,@APRINT

DIEF:	-1

PRECYC:	MOVE A,PITELE
	MOVEM A,RECYC
	CONSO DIS,2000	;LIGHT PEN
	JRST RECYC+2
	SKIPGE LTPOFF
	JRST LTPSNX
	AOS LTPCNT
	DATAI DIS,A
	ANDI A,1777
	ADDM A,LTPX
	DATAI DIS,A
	LSH A,-18.
	ADDM A,LTPY
LTPSNX:	CONO DIS,FLGCHN_3+DISCHN
	JRST RECYC2
RECYC:	0
	PUSH P,A
	SKIPE A,DISPNR
	JRST RECYC1
	MOVNI A,60.
	EXCH A,DISTM
	CAME A,DISTM
	SKIPN A,VDISLIST
	JRST DISTP
RECYC1:	HRL A,(A)
	HLRZM A,DISPNR
	HLRZ A,(A)
	MOVN A,-1(A)
	SETCAM A,BLKOP
	CONSZ DIS,7
	CONSZ DIS,7400
	CONO DIS,100+FLGCHN_3+DISCHN
RECYC2:	POP P,A
	JRST 12,@RECYC
DISTP:	CONO DIS,100
	SETZM DISTM
	JRST RECYC2

LPEN:	SETOM LTPOFF
	MOVE A,LTPY
	IDIV A,LTPCNT
	PUSHJ P,FIX1A
	MOVEM A,LTPY
	MOVE A,LTPX
	IDIV A,LTPCNT
	PUSHJ P,FIX1A
	MOVE B,LTPY
	PUSHJ P,CONS
	MOVEM A,LTPX
	MOVE A,LTPCNT
	PUSHJ P,FIX1A
	MOVE B,LTPX
	PUSHJ P,CONS
	SETZB B,LTPCNT
	MOVEM B,LTPX
	MOVEM B,LTPY
	MOVEM B,LTPOFF
	POPJ P,

LTPCNT:	0
LTPX:	0
LTPY:	0
LTPOFF:	0

;ERROR MESSAGES

ER1:	LERR MES2	;FIRST OBJECT ON INPUT LIST ILLEGAL -READ
ER2:	LERR MES3	;CONTEXT ERROR WITH DOT NOTATION -READ

QA2A:	HLRZ A,(AR1)
QA2:	PUSHJ P,EPRINT
	LERR MES5	;FUNCTION OBJECT HAS NO DEFINITION-EVAL
QA8:	PUSHJ P,EPRINT
	LERR MES6	;UNBOUND VARIABLE-EVAL

E5:	LERR MES11	;NON-NUMERIC ARGUMENT -NUMARG
E6:	LERR MES12	;NO PRINT NAME -INTERN
E7:	LERR MES13	;NO LIST -MAKNAM


QF2:	LERR MES18	;TOO MANY ARGUMENTS SUPPLIED -APPLY
QF3:	LERR MES19	;TOO FEW ARGUMENTS SUPPLIED -APPLY

QA1:	PUSHJ P,EPRINT
	LERR MES25	;FUNCTION OBJECT HAS NO DEFINITION -APPLY

EG1:	HRRZ A,T
	PUSHJ P,EPRINT
	LERR [SIXBIT \UNDEFINED PROG TAG-GO!\]

TYI:	SKIPE TAPRED
	PUSHJ P,URED"
	JRST TYIN
	POPJ P,

FORMF:	MOVEI A,15
	JSP T,SPECBIND
	0 A,TTYOFF
	PUSH P,[UNBIND]

ITYO:	PUSHJ P,NUMVAL
TYO:	PUSH P,B
	SETCM B,VLINEL
	CAIN A,15
	MOVNM B,VCHRCT
	CAIE A,11	;TAB
	JRST TOL1
	ORCMI B,7
	EXCH B,VCHRCT
	ADD B,VCHRCT
	TRZ B,7
	SUBM B,VCHRCT
TOL1:	POP P,B
	SOSG VCHRCT
	JRST ICR

	SKIPLE LPTON
	PUSHJ P,APILPT
	SKIPN TAPWRT
	JRST TYOUT
UTYO:	CAIN A,15
	JRST UCR1
	CAIN A,12
	AOSE LINF
	PUSHJ P,UCR2
TYOUT:	SKIPG TORM
	JRST .-1
	SKIPE TTYOFF
	POPJ P,
	IDPB A,TOIP
	SOS TORM
	MOVE A,TOIP
	CAMN A,[(10700)TOBE-1]
	HRRI A,TOBO-1
	MOVEM A,TOIP
STYO:	CONSO TTY,30
	CONO TTY,10+TTYCHN
	POPJ P,


UCR1:	SETOM LINF
	PUSHJ P,UCR2
	MOVEI A,12
	PUSHJ P,UCR2
	MOVEI A,15
	JRST TYOUT

UCR2:	PUSHJ P,UWR"
	LERR [SIXBITCH \TAPE FULL!\]
	POPJ P,

APILPT:	CAIE A,15
	JRST PILPT"
	PUSHJ P,PILPT
	MOVEI A,12
	PUSHJ P,PILPT
	MOVEI A,15
	POPJ P,

ICR:	PUSH P,A
	PUSHJ P,TERPRI
	POP P,A
	JRST TYO

TYIN:	ILDB A,MACCR	;OR JRST .+3
	JUMPN A,CPOPJ
	SETZM MACCR
	SETZM TAPRED
	SKIPG TICC
	JRST .-1
	MOVE A,TIOP
	CAMN A,[(10700)TIBE-1]
	MOVEI A,TIBO-1
	HRRM A,TIOP
	ILDB A,TIOP
	SOS TICC
	POPJ P,

PITELE:	0
	PUSH P,A
	CONSZ DIS,7400
	JRST PRECYC
	CONSZ TTY,10
	JRST TYP11
	CONSO TTY,40
	JRST TTYRET
	DATAI TTY,A
	ANDI A,177
	PUSHJ P,TYOB
;;;PDP-6 ONLY
	FSC A,@ECHOCC
	FSC A,@TICC
	TLNE A,200000
;;;FIX FOR PDP-10
;	MOVE B,ECHOCC
;	ADD B,TICC
;	CAIL B,TIBS
	JRST DING
	IDPB A,TIIP
PITEL2:	MOVE A,TIIP
	CAMN A,[(10700)TIBE-1]
	MOVEI A,TIBO-1
	HRRM A,TIIP
	AOSA ECHOCC
DING:	SETOM DINGF
	PUSHJ P,STYO
	JRST TTYRET

TYP11:	AOSE TYOBFC
	JRST TYP1
	MOVEI A,TOBS
	MOVEM A,TORM
	MOVE A,TOIP
	MOVEM A,TOOP
TYP1:	MOVEI A,.
	AOSN LIF
	JRST TYP3
	MOVEI A,12
	HRRM A,TYP1
	MOVEI A,7
	AOSN DINGF
	JRST TYP3
	MOVEI A,177
	AOSG SPCCC
	JRST TYP3
	MOVE A,TORM
	CAIL A,TOBS
	JRST TYP4
	MOVE A,TOOP
	CAMN A,[(10700)TOBE-1]
	MOVEI A,TOBO-1
	HRRM A,TOOP
	ILDB A,TOOP
	AOS TORM

TYPCOM:	CAIN A,15
TYP6:	SETOM LIF
	CAIN A,10
	JRST TYO1
	CAILE A,6
	CAIL A,16
	JRST TYO1
TYP3:	CAIE A,11
	JRST TYP31
	MOVE A,TICHRC
	ANDI A,7
	SUBI A,14
	ASH A,-2
	MOVEM A,SPCCC
	MOVEI A,11
TYP31:	CAIN A,12
LCHAR:	CAIE A,.
	JRST .+2
	SOSE LIF
	DATAO TTY,A
	HRRM A,LCHAR
	CAIL A,40
	AOS TICHRC
	CAIN A,15
	CLEARM TICHRC
	JRST TTYRET

TYP4:	SKIPLE ECHOCC
	JRST TYP4A
TYP5:	CONO TTY,200+TTYCHN
TTYRET:	POP P,A
	JRST 12,@PITELE


TYP4A:	MOVE A,ECHOP
	CAMN A,[(10700)TIBE-1]
	MOVEI A,TIBO-1
	HRRM A,ECHOP
	ILDB A,ECHOP
	SOS ECHOCC
	AOS TICC
	JRST TYPCOM

TYO1:	CAIL A,40
	JRST TYP3
	ADDI A,100
	HRRM A,TYP1
	MOVEI A,"^
	JRST TYP6


TYOB:	CAIN A,^W
	SETOM TYOBFC
	JRST TYOB1


CNTROL:	ANDI A,77
TYOB1:	CAIN A,7	;^G
	JSR QUIT
	CAIN A,27	;^W
	AOS TTYOFF
	CAIN A,26	;^U
	SETZM TTYOFF
	CAIN A,23	;^S
	SETZM TAPRED
	CAIN A,21	;^Q
	AOS TAPRED
	CAIN A,22	;^R
	AOS TAPWRT
	CAIN A,24	;^T
	SETZM TAPWRT
	CAIN A,3	;^C
	SETZM GCGAGV
	CAIN A,4	;^D
	AOS GCGAGV
	CAIN A,2	;^B
	AOS LPTON
	CAIN A,5	;^E
	SETZM LPTON
	POPJ P,

TOIP:	(10700)TOBO-1
TOOP:	(10700)TOBO-1
TORM:	TOBS
TIIP:	(10700)TIBO-1
TIOP:	(10700)TIBO-1
ECHOP:	(10700)TIBO-1
TICC:	0
TIBO:	BLOCK 26.
TIBE:
TIBS=<TIBE-TIBO-1>*5
TOBO:	BLOCK 40
TOBE:
TOBS=<TOBE-TOBO>*5
LIF:	0
DINGF:	0
SPCCC:	0
TICHRC:	0
ECHOCC:	0
TYOBFC:	0
LINF:	0	;UTAPE LINF

UUOH:
ERROR:	0
	MOVEM T,TSV
	MOVEM TT,TTSV
	LDB T,[331100,,40]
	CAIGE T,74
	JRST ERRA
	HLRE R,@40
	AOJN R,UUOS
	LDB T,[270400,,40]	;LISP "CALL" TYPE UUO'S
	CAILE T,15
	MOVEI R,-15(T)
	HRRZ T,@40
UUOH1:	HLRZ TT,(T)
	HRRZ T,(T)
	CAIN TT,SUBR
	JRST @UUST(R)
	CAIN TT,FSUBR
	JRST @UUFST(R)
	CAIN TT,LSUBR
	JRST @UULT(R)
	CAIN TT,EXPR
	JRST @UUET(R)
	CAIN TT,FEXPR
	JRST @UUFET(R)
	HRRZ T,(T)
	JUMPN T,UUOH1
	PUSH P,A
	PUSH P,B
	HRRZ A,40
	MOVEI B,VALUE
	PUSHJ P,GET
	JUMPN A,UUOH2
	HRRZ A,40
	PUSHJ P,EPRINT
	LERR [SIXBIT \UNDEFINED UUO!\]

UUOSBR:	HLRZ T,(T)
	MOVE TT,40
	HRLI T,(PUSHJ P,)
	TLNE TT,1000	;1000 MEANS NO PUSH
	TLCA T,(JRST#PUSHJ P,)
	PUSH P,UUOH
	SOS UUOH
UUOCL:	TLNN TT,2000
	MOVEM T,@UUOH
	MOVE TT,TTSV
	EXCH T,TSV
	JRST @TSV

UUOH2:	HRRZ TT,(A)		;FUNARG FOUND IN VALUE CELL
	POP P,B
	POP P,A
	JRST UUOEX1

UUOS:	HRRZ TT,40	;NON-ATOMIC-FUN CALLED LIKE SUBR
	JRST UUOEX1

UUOEXP:	HLRZ TT,(T)	;EXPR CALLED LIKE SUBR
UUOEX1:	LDB T,[270500,,40]
	TRZN T,20
	PUSH P,UUOH
	PUSH P,TT
	JUMPE T,IAPPLY
	MOVNS T
	HRLZ TT,T
	PUSH P,A(TT)
	AOBJN TT,.-1
	JRST IAPPLY

TTSV:	0
TSV:	0

ARGPDL:	LDB T,[270400,,40]	;ARGS => PDL  -CNT=> T
	MOVNS T
	HRLZ R,T
ARGP1:	JUMPE R,(TT)
	PUSH P,A(R)
	AOBJN R,.-1
	JRST (TT)

QTIFY:	PUSHJ P,NCONS
	MOVEI B,CQUOTE
	JRST XCONS

QTLFY:	MOVEI A,0
QTLFY1:	JUMPE T,(TT)
	EXCH A,(P)
	PUSHJ P,QTIFY
	POP P,B
	PUSHJ P,CONS
	AOJA T,QTLFY1

PDLARG:	JRST .+NACS+2(T)
	REPEAT NACS+1,POP P,A+NACS-.RPCNT
	JRST (TT)

NOUUO:	MOVSI B,(TLNN T,)
	SKIPE A
	MOVSI B,(TLNA)
	HLLM B,UUOCL
	POPJ P,

;R=0 => COMPILER CALLING A -
;R=1 => COMPILER CALLING A LSUBR
;R=2 => COMPILER CALLING F TYPE
UUST:	UUOSBR
	UUOS1	;CALLING L ITS A SUBR
	UUOS2	;CALLING F
UUFST:	UUOS9	;CALLING - ITS A F
	UUOS10	;CALLING L
	UUOSBR
UULT:	UUOS7	;CALLING - ITS A L
	UUOSBR
	UUOS8
UUET:	UUOEXP
	UUOS5	;CALLING L ITS AN EXPR
	UUOS6	;CALLING F ITS AN EXPR
UUFET:	UUOS3	;CALLING - ITS A FEXPR
	UUOS4	;CALLING L
	UUOEX2

UUOEX2:	TLO A,400000
	MOVEI B,1
	DPB B,[270400,,40]
	JRST UUOEXP

UUOS1:	HLRZ R,(T)	;SUBR CALLED LIKE LSUBR
	MOVE T,TSV
	JSP TT,PDLARG
	JRST (R)

UUOS3:	PUSH P,(T)
	JSP TT,ARGPDL
UUOS4A:	JSP TT,QTLFY
	MOVEI TT,1
	DPB TT,[270400,,40]
UUOS6A:	POP P,TT
	HLRZS TT
	JRST UUOEX1

UUOS4:	PUSH P,(T)
	MOVE T,TSV
	JRST UUOS4A

UUOS5:	HLRZ R,(T)
	MOVE T,TSV
	JSP TT,PDLARG
	MOVE TT,R
	JRST UUOEX1

UUOS6:	PUSH P,(T)
	PUSH P,UUOH
	PUSH P,40
	JSP TT,ILIST
	JSP TT,PDLARG
	POP P,40
	POP P,UUOH
	JRST UUOS6A

UUOS8:	SKIPA TT,CILIST
UUOS7:	MOVEI TT,ARGPDL
	HRRM TT,UUOS7A
	MOVE TT,40
	TLNN TT,1000
	PUSH P,UUOH
	HLRZ TT,(T)
UUOS7A:	JRST ARGPDL

UUOS9:	PUSH P,T
	JSP TT,ARGPDL
UUS10A:	JSP TT,QTLFY
	MOVSI T,(LER2)
	IORM T,40
	POP P,T
	JRST UUOSBR

UUOS10:	PUSH P,T
	MOVE T,TSV
	JRST UUS10A

UBD:	CAMN SP,B
	POPJ P,
	PUSHJ P,UNBIND
	JRST UBD

ERRA:	CONO APR,410000+APRCHN
	LDB A,[331100,,40]
	CAIN A,4
	JRST STRTYP
	CAIG A,4
QQ=10
	JRST QQ,.+1(A)
	JRST QQ,BADERR	;PROG TRAPPED - ILLEGAL OPERATION
	JRST QQ,ERROR1	;ORDINARY LISP ERROR - ERRSETTABLE
	JRST QQ,ERRORG	;PRINT OUT FOR AN UPCOMING ERROR
	JRST QQ,ERROR2

ERRORG:	MOVE P,C2
	SETZB AR1,AR2A
	SETZB B,C
	SETZB T,TT
	SETOM ERRSW
	SETZM ERRTN
ERROR1:	SKIPN ERRSW
	JRST ERSET1
	SETZM TTYOFF
	PUSHJ P,TERPRI
ERR1A:	MOVSI A,440600
	HRR A,40
	MOVEM A,ERROR
ERRORB:	ILDB A,ERROR
	CAIN A,1
	JRST ERSET1
	CAIN A,77
	TRCA A,62
	ADDI A,40
	PUSHJ P,TYO
	JRST ERRORB

BADERR:	MOVE P,C2
	PUSHJ P,TERPRI
	HRRZ TT,ERROR
	HRRM TT,PRINL1
	PUSHJ P,PRINL2
	HRRZ R,PRINL1
	STRT [SIXBIT \_PROGRAM TRAPPED FROM !\]
	PUSHJ P,ERRR2A
	JRST LOSP

QUIT:	0
	SETOM BELFLG
	JUMPGE NIL,@QUIT
	SETZM ERRTN
	SETZM BELFLG
	SETOM ERRSW
	LERR MES1

BELFLG:	0
ERRTN:	0	;0 => TOP LEVEL
	;- => PDL TO RESET TO - STORED BY ERRORSET
ERRSW:	-1	;0 MEANS NO PRNT ON ERROR

STRTYP:	PUSH P,ERROR	;STRING TYPEOUT UUO.
	PUSH P,ERRTN
	HRRZM P,ERRTN
	JRST ERR1A

BACTRF:	0

ERROR2:	SKIPN ERRSW
	JRST ERSET1
	PUSH P,ERRO2E+1
	PUSHJ P,TERPRI
	HRRZ R,40
ERRR2A:	MOVEI A,[-1,,[PNAME,,[[[ASCII \?\],,0],,0]]]
	HRROI NIL,CNIL2
	MOVEI AR2A,LISP
	MOVSI B,-77
ERRO2F:	HLRZ C,OBTBL(B)
ERRO2B:	JUMPE C,ERRO2A
	HLRZ TT,(C)
ERRO2C:	HRRZ TT,(TT)
	JUMPE TT,ERRO2G
	HLRZ AR1,(TT)
	CAIN AR1,LSUBR
	JRST ERRO2H
	CAIE AR1,SUBR
	CAIN AR1,FSUBR
	JRST ERRO2H
	HRRZ TT,(TT)
	JRST ERRO2C

ERRO2A:	AOBJN B,ERRO2F
ERRO2D:	PUSHJ P,PRINC
	MOVEI A,MES22
ERRO2E:	HRRM A,40
	POPJ P,ERROR1
ERRO2H:	HRRZ TT,(TT)
	HLRZ TT,(TT)
	CAMLE TT,AR2A
	CAMLE TT,R
	JRST ERRO2G
	MOVE AR2A,TT
	HLRZ A,(C)
ERRO2G:	HRRZ C,(C)
	JRST ERRO2B

BACRTN:	MOVEI A,0
ERR:	SKIPN ERRTN
	JRST LSPRET	;TOP LEVEL ERROR
	MOVE P,ERRTN
ERR1:	POP P,B
	PUSHJ P,UBD	;RESTORE CONDITIONS AND PROCEED
	POP P,ERRSW
	POP P,ERRTN
	JRST ERRP4

ERRSET:	PUSH P,PA3	;"ERRSET" PUSH
	PUSH P,PA4
	PUSH P,ERRTN
	PUSH P,ERRSW
	PUSH P,SP
	MOVEM P,ERRTN
	HRRZ C,(A)
	HLRZ C,(C)
	MOVEM C,ERRSW
	HLRZ A,(A)
	PUSHJ P,EVAL
	PUSHJ P,NCONS	;NORMAL EXIT
	JRST ERR1

ERSET2:	POP P,ERRTN
	POPJ P,

MES1:	SIXBIT \QUIT!\
MES2:	SIXBIT \ILLEGAL OBJECT - READ!\
MES3:	SIXBIT \DOT CONTEXT ERROR!\
MES5:	SIXBIT \UNDEFINED FUNCTION!\
MES6:	SIXBIT \UNBOUND VARIABLE - EVAL!\
MES11:	SIXBIT \NON-NUMERIC ARGUMENT!\
MES12:	SIXBIT \NO PRINT NAME - INTERN!\
MES13:	SIXBIT \NO LIST-MAKNAM!\
MES18:	SIXBIT \TOO MANY ARGUMENTS SUPPLIED - APPLY!\
MES19:	SIXBIT \TOO FEW ARGUMENTS SUPPLIED - APPLY!\
MES22:	SIXBIT \MADE ILLEGAL MEMORY REFERENCE!\
MES25:	SIXBIT \UNDEFINED FUNCTION - APPLY!\

ERSET1:	SKIPLE ERRTN
	JRST ERSET2
BKTRC1:	SKIPE ERRSW
	SKIPN BACTRF
	JRST BACRTN
BAKTRC:	HRRZ D,P
	SETOM BACTYF'
BKTR2:	HRRZ A,C2
	HRRZ B,1(A)
	CAIN B,PPEVAL+1
	CAMG D,A
	JRST BACRTN
	AOSN BACTYF
	STRT [SIXBIT \_BACKTRACE_!\]
	HRRZ A,(D)
BPSH:	CAIGE A,.
BPSL:	CAIGE A,.
	CAIGE A,FS
	JUMPN A,.+2
	SOJA D,BKTR2
	CAIN A,ILIST3
	JRST BKTR1A
BKTR1B:	CAIN A,CPOPJ
	JRST BKTR1C
	HLRZ B,-1(A)
	TRZ B,(@)
	CAILE B,77000	;CALL TYPE UUOS
	CAIN B,(PUSHJ P,)
	CAIGE B,74000
	JRST BKTR1
	MOVEI R,@-1(A)
	PUSH P,R
BKTR4:	MOVEI R,-1(A)
	PUSHJ P,ERRR2A
	MOVEI A,"-
	PUSHJ P,TYO
	POP P,R
	PUSHJ P,ERRR2A
	MOVEI A,40
	PUSHJ P,TYO
BKTR1:	SOJA D,BKTR2
BAKGAG:	MOVEM A,BACTRF
	POPJ P,

BKTR1A:	HRRZ B,-1(D)	;PROBABLY FOR ENTRY TO SOME SUBR, LSUBR, OR EXPR
	CAIE B,EXP2
	CAIN B,ESB1
	JRST .+2
	JRST BKTR1B
	HLRE B,-1(D)
	ADD B,D
	HLRZ A,-3(B)
	JUMPE A,BKTR1
	PUSHJ P,PRINC
	XCT "-,CTY
	STRT [SIXBIT \EVALARGS !\]
	JRST BKTR1

BKTR1C:	HLRZ A,(D)	;PROBABLY ENTERED AN F-TYPE FUNCTION
	PUSHJ P,PRINC
	XCT "-,CTY
	STRT [SIXBIT \ENTER !\]
	JRST BKTR1

UINIT2:	SKIPE T
	SKIPA A,-1(P)
	SKIPA A,CRUNIT
	POP P,-1(P)
	AOJL T,QF2
UINIT1:	MOVEM A,CRUNIT
	PUSHJ P,NUMVAL
	PUSHJ P,FILEST"
	LERR [SIXBIT \TOO MANY FILE DIRECTORIES!\]
	POPJ P,

CRUNIT:	3

SIXMAK:	MOVSI C,(SIXBIT \@\)
	MOVEM C,SIXMK2
	MOVE AR1,[440600,,SIXMK2]
	HRROI R,SIXMK1
	PUSHJ P,PRINTA
	MOVE A,SIXMK2
	POPJ P,

UREAD:	PUSH P,A
	PUSHJ P,CADDR
	CAIN A,-1
	MOVE A,CRUNIT
	PUSHJ P,EVAL
	PUSHJ P,UINIT1
	POP P,A
	PUSHJ P,UFNAME
	PUSHJ P,OPNRD"
	LERR [SIXBIT \FILE NOT FOUND!\]
UEXIT:	MOVE A,CRUNIT
	POPJ P,

UWRITE:	PUSHJ P,UINIT2
	PUSHJ P,OPNWR"
	LERR [SIXBITCH \TAPE FULL!\]
	JRST FIX1A

UFILE:	PUSHJ P,UFNAME
	SETZM TAPWRT
	PUSHJ P,FILE"
	JRST FALSE
	LDB A,[270400,,.WBYTS"]
	AOS A
	MOVEM A,CRUNIT
	POPJ P,

UKILL:	PUSHJ P,UINIT2
	PUSHJ P,TAPKIL"
	JRST FIX1A

UFNAME: PUSH P,A
	HLRZ A,(A)
	PUSHJ P,SIXMAK
	EXCH A,(P)
	PUSHJ P,CADR
	PUSHJ P,SIXMAK
	MOVE B,A
	JRST POPAJ

SIXMK1:	ADDI A,40
	CAIN A,77
	MOVEI A,0
	TLNE AR1,770000
	IDPB A,AR1
	POPJ P,

SIXMK2:	SIXBIT \@\

VIDI:	JSP T,SPECBIND
	VDISLIST
	CONO VID,10
	CONSZ DIS,77
	JRST .-1
	CONO DIS,100
	DPB B,[001200,,VIDI1]
	DATAO DIS,VIDI1
	DPB A,[221200,,VIDI2]
	CONSO DIS,200
	JRST .-1
	DATAO DIS,VIDI2
	MOVEI A,1000
	CONSO DIS,200
	JRST .-1
	CONSO VID,10
	SOJG A,.-1
	DATAI VID,A
	CAIGE A,2
	ADDI A,400
	SOJA A,UNBIND

VIDI1:	20135,,220000
VIDI2:	2000,,0
VIDLIN:	PUSHJ P,VIDI
	MOVEI B,400
	IDIV B,A
	MOVE A,B
	POPJ P,

VIDLOG:	PUSHJ P,VIDI
	TLO A,232000
	FAD A,A
	FMPR A,[0.035]
	REPEAT 3,FMPR A,A
	MOVEM A,B
	REPEAT 2,FMPR A,A
	FMPR A,B
	ASH A,-33
	MOVNS A
	ADDI A,400
	POPJ P,

CADDDR:	SKIPA A,(A)
CADDAR:	HLRZ A,(A)
CADDR:	SKIPA A,(A)
CADAR:	HLRZ A,(A)
CADR:	SKIPA A,(A)
CAAR:	HLRZ A,(A)
QUOTE:
CAR:	HLRZ A,(A)
	POPJ P,

CDDDDR:	SKIPA A,(A)
CDDDAR:	HLRZ A,(A)
CDDDR:	SKIPA A,(A)
CDDAR:	HLRZ A,(A)
CDDR:	SKIPA A,(A)
CDAR:	HLRZ A,(A)
CDR:	HRRZ A,(A)
	POPJ P,

CAADDR:	SKIPA A,(A)
CAADAR:	HLRZ A,(A)
CAADR:	SKIPA A,(A)
CAAAR:	HLRZ A,(A)
	JRST CAAR

CDADDR:	SKIPA A,(A)
CDADAR:	HLRZ A,(A)
CDADR:	SKIPA A,(A)
CDAAR:	HLRZ A,(A)
	JRST CDAR

CAAADR:	SKIPA A,(A)
CAAAAR:	HLRZ A,(A)
	JRST CAAAR

CDDADR:	SKIPA A,(A)
CDDAAR:	HLRZ A,(A)
	JRST CDDAR

CDAADR:	SKIPA A,(A)
CDAAAR:	HLRZ A,(A)
	JRST CDAAR

CADADR:	SKIPA A,(A)
CADAAR:	HLRZ A,(A)
	JRST CADAR

PNGNK1:	PUSHJ P,NCONS
	MOVEI B,PNAME
	PUSHJ P,XCONS
ACONS:	TROA B,-1
NCONS:	TRZA B,-1
XCONS:	EXCH B,A
CONS:	AOS CONSVA
	HRL B,A
AGC2:	SKIPN A,F
	JRST AGC1
	MOVE F,(F)
	MOVEM B,(A)
	POPJ P,

AGC1:	HLR A,B
	PUSHJ P,AGC
	JRST AGC2

PATOM:	CAIGE A,@GCP1
ATOM:	CAIG A,INUM
	JRST TRUE
	HLLE A,(A)
	AOJE A,TRUE
	JRST FALSE

EQ:	CAMN A,B
	JRST,TRUE
	JRST FALSE

EQ2:	CAILE T,INUM
	CAIG TT,INUM
	JRST FALSE
	HRRZ T,(T)
	HLRZ A,(T)
	HRRZ T,(T)
	MOVE T,(T)
EQ3:	HRRZ TT,(TT)
	HLRZ B,(TT)
	HRRZ TT,(TT)
	MOVE TT,(TT)
EQ4:	CAIE A,FIXNUM
	CAIN A,FLONUM
	CAME A,B
	JRST FALSE
	CAMN T,TT
	JRST TRUE
	JRST FALSE

LENGTH:	MOVEI B,0
LNGTH1:	CAIG A,INUM
	JRST FIX1
	HLLE C,(A)
	AOJE C,FIX1
	HRRZ A,(A)
	AOJA B,LNGTH1

CONSVA:	0

RPLACA:	HRLM B,(A)
	POPJ P,

RPLACD:	HRRM B,(A)
	POPJ P,

ZEROP:	SOJN A,FALSE
NOT:
ANULL:	JUMPN A,FALSE
TRUE:	MOVEI A,TRUTH
	POPJ P,

MINUS:	PUSHJ P,NUMVAL
	MOVNS A
MAKNUM:	CAIE B,FIXNUM
	JRST MAKNM1
MAKNM3:	JUMPL A,MAKNM1
MAKNM2:	CAIGE A,INUM
	AOJA A,CPOPJ
MAKNM1:	PUSHJ P,FWCONS
	JRST ACONS-1

FW0CNS:	MOVEI A,0
FWCONS:	JUMPN FF,FWC1
	EXCH A,FWC0
	PUSHJ P,AGC
	EXCH A,FWC0
FWC1:	EXCH A,(FF)
	EXCH A,FF
	POPJ P,

FWC0:	0

SASSOC:	PUSHJ P,SAS1
	JCALLF 0,(C)
	POPJ P,
SAS0:	HRRZ B,(B)
SAS1:	JUMPE B,CPOPJ
	MOVS T,(B)
	MOVS TT,(T)
	CAIE A,(TT)
	JRST,SAS0
	HRRZ A,T
CPOPJ1:	AOS (P)
	POPJ P,

ASSOC:	PUSHJ P,SAS1
FALSE:	MOVEI A,0
CPOPJ:	POPJ P,

REVERSE:	MOVEI B,0
	JUMPE A,PROG2
	HRRZ T,(A)
	HLRZ A,(A)
	PUSHJ P,CONS
	MOVE B,A
	MOVE A,T
	JRST REVERSE+1

REMPROP:	HRRZ T,(A)
	JUMPE T,FALSE
	MOVS TT,(T)
	CAIN B,(TT)
	JRA TT,REMP1
	HRRZ A,(T)
	JRST REMPROP
REMP1:	HRRM TT,(A)
	JRST TRUE

GET0:	HRRZ A,(D)
GET:	HRRZ D,(A)
	JUMPE D,FALSE
	HLRZ A,(D)
	CAME A,B
	JRST GET0
	HRRZ D,(D)
	HLRZ A,(D)
	POPJ P,

GETL0:	HRRZ A,(T)
GETL:	HRRZ T,(A)
	JUMPE T,FALSE
	HLRZ A,(T)
	MOVE C,B
GETL1:	JUMPE C,GETL0
	HLRZ TT,(C)
	CAMN TT,A
	JRST GETL2
	HRRZ C,(C)
	JRST GETL1
GETL2:	MOVE A,T
	POPJ P,

.NCONC:	JUMPE A,PROG2
	MOVE TT,A
	MOVE C,TT
	HRRZ TT,(C)
	JUMPN TT,.-2
	HRRM B,(C)
	POPJ P,

NUMBER:	CAIG A,INUM
	JUMPN A,TRUE
	HLLE T,(A)
	AOJN T,FALSE
	HRRZ A,(A)
	HLRZ A,(A)
	CAIE A,FIXNUM
	CAIN A,FLONUM
	JRST TRUE
	JRST FALSE

PUTPROP:	HLRZ T,(A)	;ATOM,VALUE,INDICATOR
	CAIE T,-1
	LERR [SIXBITCH \FIRST ARGUMENT NONATOMIC - PUTPROP\]
	MOVE T,A
CSET3:	HRRZ A,(A)
	JUMPE A,CSET2
	HLRZ TT,(A)
	HRRZ A,(A)
	CAIE TT,(C)
	JRST CSET3
	CAIE C,VALUE
	JRST CSET1
	HRRZ T,(B)
	HLRZ A,(A)
	HRRM T,(A)
	JRST PROG2
CSET1:	HRLM B,(A)
PROG2:	MOVE A,B
	POPJ P,
CSET2:	HRRZ A,(T)
	PUSHJ P,XCONS
	HRRZ B,C
	PUSHJ P,XCONS
	HRRM A,(T)
	JRST CADR

DEFPROP:	HRRZ B,(A)
	HRRZ C,(B)
	HLRZ A,(A)
	HLRZ B,(B)
	HLRZ C,(C)
PTPRP1:	PUSH P,A
	PUSHJ P,PUTPROP
	JRST POPAJ

PA3:	0
PA4:	0

PROG:	PUSH P,PA3
	PUSH P,PA4
	HLRZ TT,(A)
	HRRZ A,(A)
	HRROM A,PA4
	MOVEM A,PA3
	JUMPE TT,PG0
	MOVSI C,1
	MOVEI B,VALUE
	MOVEM SP,SPSV
	ANDCAM C,PA4

PG7A:	HLRZ A,(TT)
	MOVEI AR1,0
	PUSHJ P,BIND
	HRRZ TT,(TT)
	JUMPN TT,PG7A
	PUSH SP,SPSV

PG0:	SKIPA T,PA3
PG5A:	MOVE T,A
PG1:	JUMPE T,PG2
	HLRZ A,(T)
	HRRZ T,(T)
	HLLE B,(A)
	AOJE B,PG1
	MOVEM T,PA3
	PUSHJ P,EVAL
	SKIPL A,PA4
	JRST PG4
PG1A:	SKIPL T,PA3
	JRST PG1
PG5:	JUMPE A,EG1
PG5B:	HLRZ TT,(A)
	HRRZ A,(A)
	CAIN TT,(T)
	JRST PG5A
	JRST PG5

PG2:	TDZA A,A
PG4:	HRRZS A
	MOVSI B,1
	TDNN B,PA4
	PUSHJ P,UNBIND
ERRP4:	POP P,PA4
	POP P,PA3
	POPJ P,

SPSV:	0

GO:	HLRZ A,(A)
	HRROM A,PA3
	HLLE B,(A)
	AOJE B,FALSE
	PUSHJ P,EVAL
	JRST GO+1

RETURN:	HLL A,PA4
	TLZ A,-2
	MOVEM A,PA4
	POPJ P,

BIND:	PUSH P,B	;USES ONLY A,B,TT. MUST PRESERVE T,
	HRRZM A,BIND3	; SEE IAP2, APLBL, ETC
BIND2:	MOVEI B,VALUE	;BIND ATOM IN A TO VALUE IN AR1,SAVE
	PUSHJ P,GET	;OLD BINDING ON S PDL
	JUMPE A,BIND1	;ADD VALUE CELL
	PUSH SP,(A)
	HRLM A,(SP)
	HRRZM AR1,(A)
POPBJ:	POP P,B
	POPJ P,

BIND1:	MOVEI B,UNBOUND
	MOVEI A,0
	PUSHJ P,CONS
	HRRZ B,@BIND3
	PUSHJ P,CONS
	MOVEI B,VALUE
	PUSHJ P,XCONS
	HRRM A,@BIND3
	MOVE A,BIND3
	JRST BIND2

BIND3:	0

SETQ:	HLRZ B,(A)
	PUSH P,B
	PUSHJ P,CADR
	PUSHJ P,EVAL
	MOVE B,A
	POP P,A
SET:	MOVE AR1,B
	PUSHJ P,BIND
	SUB SP,[(1)1]
	MOVE A,AR1
	POPJ P,

CON2:	HRRZ A,(T)
COND:	JUMPE A,CPOPJ	;ENTRY
	PUSH P,A
	HLRZ A,(A)
	HLRZ A,(A)
	PUSHJ P,EVAL
	POP P,T
	JUMPE A,CON2	;IF FIRST OF COND PAIR IS TRUE
	HLRZ T,(T)
COND2:	HRRZ T,(T)
	JUMPE T,CPOPJ	;LOOP FOR GENERALIZED COND PAIR
	PUSH P,T
	HLRZ A,(T)
	PUSHJ P,EVAL
CPOPT:	POP P,T
	JRST COND2

IRPS OP,,DIF QUO,ERATOR,,FERENCE TIENT,FIX,,SUB IDIV,FLO,,FSB FDV
.!OP:	PUSH P,A
	PUSH P,B
	MOVNI T,2
OP!ERATOR:
	MOVE R,[JRST OP!2]
	MOVE C,R
	SOJA C,OP!1

	SKIPA C,[FLO!R TT,A]
OP!2:	MOVE C,[FIX TT,A]
	MOVEM C,PLUS3
	MOVE C,[FLO!R TT,A]
	MOVEM C,PLUS6
	MOVE TT,A
	JRST PLUS4
TERMIN

.PLUS:	PUSH P,A
	PUSH P,B
	MOVNI T,2
PLUS:	MOVE R,[ADD TT,A]
	MOVE C,[FADR TT,A]
DIF1:	MOVEI TT,0
PLUS1:	MOVEM R,PLUS3
	MOVNM T,PLUS8
	ADD T,P
	MOVEI R,FIXNUM
PLUS7:	MOVEM C,PLUS6
	HRLS PLUS8
	JRST PLUS4
PLUS5:	MOVE R,PLUS6	;FAD TT,A OR FMP TT,A
	MOVEM R,PLUS3
	MOVEI R,FLONUM
	EXCH TT,A
	PUSHJ P,FLOAT
	EXCH TT,A
PLUS3:	ADD TT,A
PLUS4:	CAMN T,P
	JRST PLUS2
PLUS9:	MOVE A,1(T)
	PUSHJ P,NUMVAL
	CAMN B,R
	AOJA T,PLUS3
	CAIE R,FLONUM
	AOJA T,PLUS5
	PUSHJ P,FLOAT
	AOJA T,PLUS3

PLUS2:	MOVE A,TT	;EXIF FROM "PLUS" SERIES
	MOVE B,R
	SUB P,PLUS8
	JRST MAKNUM

.TIMES:	PUSH P,A
	PUSH P,B
	MOVNI T,2
TIMES:	MOVE R,[IMUL TT,A]
	MOVE C,[FMPR TT,A]
QUO1:	MOVEI TT,1
	JRST,PLUS1

PLUS6:	0
PLUS8:	0

NUMVAL:	MOVEM A,NUMV1
	JUMPE A,NUMV2
	CAIG A,INUM
	SOJA A,NUMAG1
	HRRZ A,(A)
	HLRZ B,(A)
	HRRZ A,(A)
	CAIE B,FIXNUM
	CAIN B,FLONUM
DISHD1:	SKIPA A,(A)
NUMV2:	SKIPA A,NUMV1
	POPJ P,
	PUSHJ P,EPRINT
	JRST E5

NUMV1:	0

NUMAG1:	MOVEI B,FIXNUM
	POPJ P,

FLOAT:	IDIVI A,400000
	SKIPE A
	TLC A,254000
	TLC B,233000
	FAD A,B
	POPJ P,

FIX:	PUSHJ P,NUMVAL
	CAIE B,FLONUM
	JRST FIX1A
	MULI A,400
	TSC A,A
	ASH B,-243(A)
FIX1:	MOVE A,B
FIX1A:	MOVEI B,FIXNUM
	JRST MAKNM3

LAST:	HRRZ B,(A)
	CAIG B,INUM
	POPJ P,
	HLLE B,(B)
	AOJE B,CPOPJ
	HRRZ A,(A)
	JRST LAST

ADD1:	PUSHJ P,NUMVAL
	CAIN B,FIXNUM
	AOJA A,MAKNUM
	FAD A,[1.0]
	JRST MAKNUM

SUB1:	PUSHJ P,NUMVAL
	CAIN B,FIXNUM
	SOJA A,MAKNUM
	FSB A,[1.0]
	JRST MAKNUM

MINUSP:	PUSHJ P,NUMVAL
	JUMPGE A,FALSE
	JRST TRUE

ABS:	PUSHJ P,NUMVAL
	MOVMS A
	JRST MAKNUM

.GREAT:	EXCH A,B
.LESS:	PUSH P,A
	PUSH P,B
	MOVNI T,2
LESSP:	SKIPA A,[CAML TT,]
GREATERP:
	MOVSI A,(CAMG TT,)
	HLLM A,GRESS
	MOVE C,[JRST GRESS]
	MOVEM C,PLUS3
	MOVNM T,PLUS8
	AOJGE T,QF3
	ADD T,P
	MOVE A,(T)
	PUSHJ P,NUMVAL
	MOVE TT,A
	MOVE R,B
	JRST PLUS7

GRESS:	CAMG TT,A
	JRST GRUSE
	MOVE TT,A
	CAME T,P
	JRST PLUS9
	TDZA A,A
GRUSE:	MOVEI A,FALSE-TRUE
	SUB P,PLUS8
	JRST TRUE(A)

TIME:	SKIPA A,TEMPUS
SPEAK:	MOVE A,CONSVA
	JRST FIX1A

TEMPUS:	0

SETTIME:	PUSHJ P,NUMVAL
	MOVEM A,TEMPUS
	JRST MAKNUM

REMAINDER:	PUSH P,B
	PUSHJ P,NUMVAL
	EXCH A,(P)
	PUSHJ P,NUMVAL
	POP P,B
	IDIV B,A
	MOVE A,C
	MOVEI B,FIXNUM
	JRST MAKNM3

BLKOP:	0
DISPNR:	0

	;GARBAGE COLLECTOR

GC:	PUSHJ P,AGC
	JRST FALSE

AGC:	MOVEM R,RGC
AGC3==.				;XXX actually not done like this
	PUSH P,PA3
	PUSH P,PA4
	PUSH P,NILPTR
	PUSH P,MKNAM3
	PUSH P,GCMKL
	PUSH P,DISPNR
	PUSH P,BIND3
AGCSUB==.-AGC3
	PUSH P,[GCP6]
GCP4:	MOVEI S,0
	BLT S,0
GCP2:	SETZB NIL,0	;ZEROS FIRST OF BIT TABLE
	MOVE A,C3GC
GCP5:	BLT A,0
	SKIPN GCGAGV
	JRST GCP5A
	SKIPN F
	STRT [SIXBIT \_FREE STG EXHAUSTED!\]
	SKIPN FF
	STRT [SIXBIT \_FULL WORD SPACE EXHAUSTED!\]

GCP5A:	MOVEI TT,A
GCP3:	MOVEI C,0
GCP6B:	MOVE S,P
	HLL C,P
	MOVEI B,0
GC1:	CAMN C,S
	POPJ P,
	HRRZ A,(C)

GCP:	CAIGE A,0
	CAIGE A,FS
	JRST GCEND

GCP1:	CAIL A,0
	JRST,GCMFWS
	MOVE F,(A)
	LSHC A,-5
	ROT B,5
	MOVE AR1,GCBT(B)
GCBTP2:	TDOE AR1,.(A)	;BIT TAB- (FS_-5)
	JRST GCEND
GCBTP1:	MOVEM AR1,.(A)	;BIT TAB- (FS_-5)
	PUSH P,F
	HLRZ A,F
	JRST,GCP

GCMFWS:	MOVEI AR1,.(A)	;-FWS
	IDIVI AR1,36.
	MOVNS AR2A
	LSH AR2A,36
	ADD AR2A,C2GC
	DPB TT,AR2A
GCEND:	CAMN P,S
	AOJA C,GC1
	POP P,A
	HRRZS A
	JRST GCP

C2GC:	(430100+AR1)
C3GC:	0
GCBT:	REPEAT 32., SETZ_-.RPCNT

GCP6:	HRRZ R,SC2
GCP6C:	CAIL R,(SP)
	JRST GCP6A
	PUSH P,(R)
	HRRZ C,P
	PUSHJ P,GCP6B
	SUB P,[(1)1]
	AOJA R,GCP6C

GCP6A:	HRRZ R,GCMKL
GCP6D:	JUMPE R,GCSWP
	HLRZ A,(R)
	MOVE D,(A)
GCP6E:	PUSH P,(D)
	HRRZ C,P
	PUSH P,(D)
	MOVSS (P)
	PUSHJ P,GCP6B
	SUB P,[(2)2]
	AOBJN D,GCP6E
GCP6F:	HRRZ R,(R)
	JRST GCP6D

GCFSWPP:		;SWEEP, RELOCATED TO AC'S.
OFFSET -.
GFSP1==.
	JUMPL S,.+3
	HRRZM F,(R)
	HRRZ F,R
	ROT S,1
	AOBJN R,.-4
	MOVE S,(D)
	HRLI R,-32.
	AOBJN D,GFSP1
LPROG==.
	JRST GFSPR
OFFSET 0

GCSWP:	MOVSI R,GCFSWPP	;RELOCATE INNER LOOP TO AC'S.
	BLT R,LPROG	;FOR SWEEP.
	MOVEI F,0
	MOVE D,C3GCS
	MOVEI R,FS
GCBTL1:	HRLI R,.
	MOVE S,(D)
GCBTL2:	ROT S,.
	AOBJN D,GFSP1	;COMMENCE SWEEP
GFSPR:	MOVE A,C1GCS
	MOVE B,C2GCS
	PUSHJ P,GCS0
	SKIPN GCGAGV
	JRST GCSP1
	PUSHJ P,TERPRI
	MOVE A,F
	PUSHJ P,STGPNT
	STRT [SIXBIT \ FREE STG,!\]
	MOVE A,FF
	PUSHJ P,STGPNT
	STRT [SIXBIT \ FULL WORDS AVAILABLE_!\]
GCSP1:	HRLZI S,0
	BLT S,NACS+2
	SUB P,[(AGCSUB)AGCSUB]
	JUMPE F,[LER2 [SIXBIT \NO FREE STG LEFT!\]]
	JUMPE FF,[LER2 [SIXBIT \NO FW STG LEFT!\]]
	MOVE R,RGC
	SETOM DIEF
	AOSN BELFLG
	JSR QUIT
	POPJ P,

RGC:	0

GCS0:	MOVEI FF,0
GCS1:	ILDB T,B
	JUMPN T,GCS2
	HRRZM FF,(A)
	HRRZ FF,A
GCS2:	AOBJN A,GCS1
	POPJ P,

C1GCS:	0
C2GCS:	100,,0
C3GCS:	0	;-N WDS IN BT,,BT

STGPNT:	HLLZS PRINL1	;GCGAG PRINTOUT
	JUMPE A,PRINL2
	HRRZ A,(A)
	AOS PRINL1
	JRST .-3

EPRINT:	SKIPN ERRSW
	POPJ P,
	SETZM TTYOFF

PRINT:	MOVEI R,TYO	;UTILIZES DIRECTLY OR INDIRECTLY
CTY:	JSR 15,TYOI	;A,B,C,TT,R,D,P,T
	PUSHJ P,PRIN1	;EXPECTS C,R SAFE OVER
	XCT " ,CTY	;THE "TYO" ROUTINE
	POPJ P,
PRIN1:	SKIPA R,CTYO
PRINC:	HRROI R,TYO
	PUSH P,A
	PUSHJ P,PRINTA	;THE "TYO" ROUTINE GENERALLY EXPECTS
	JRST POPAJ	;AR1,AR2A SAVE OVER PRINT
PRINTA:	PUSH P,A
	PUSHJ P,PATOM
	JUMPN A,PRINT1
	XCT "(,CTY
PRINT3:	HLRZ A,@(P)
	PUSHJ P,PRINTA
	HRRZ A,@(P)
	JUMPE A,PRINT2
	MOVEM A,(P)
	XCT CTY
	PUSHJ P,PATOM
	JUMPE A,PRINT3
	XCT ".,CTY
	XCT " ,CTY
	PUSHJ P,PRIN1A
PRINT2:	XCT "),CTY
	JRST POPAJ
PRINT1:	PUSHJ P,PRIN1A
	JRST POPAJ

PRIN1A:	MOVE A,-1(P)
	HRRM A,PRINL1
	CAIL A,@GCP1
	JRST PRINL
	CAIG A,INUM
	SOJGE A,PRINI
PRIN1B:	HRRZ A,@-1(P)
	JUMPE A,PRINL
	HLRZ B,(A)
	HRRZ A,(A)
	CAIN B,PNAME
	JRST PRINN
	CAIN B,FIXNUM
	JRST PRINI1
	CAIN B,FLONUM
	JRST PRINO
	MOVEM A,-1(P)
	JRST PRIN1B

PRINL2:	SKIPA R,CTYO	;PRINT NONSTANDARD POINTER
PRINL:	XCT "#,CTY
PRINL1:	MOVEI A,.
	MOVEI C,10+1
	JRST PRINI3

PRINI1:	MOVE A,(A)
PRINI:	HRRZ C,VBASE	;PRINT INTEGER
	JUMPGE A,PRINI2
	XCT "-,CTY
	MOVNS A
PRINI2:	MOVEI B,".-"0
	HRLM B,(P)
	CAIN C,10.+1
	SKIPE V.NOPOINT
	JRST .+2
	PUSH P,PRINI4
PRINI3:	LSHC A,-43
	LSH B,-1
	DIVI A,-1(C)
	HRLM B,(P)
	SKIPE A
	PUSHJ P,PRINI3
PRINI4:	JRST FP7A1

		;DISPLAY ROUTINES

DISHD:	MOVEI B,SUBR
	PUSHJ P,GET
	SOJA A,DISHD1
DISINI:	PUSHJ P,DISHD
	MOVE C,[373737,,373737]
	SOS A
	PUSH A,[020136,,221700]
	PUSH A,[060000,,373737]
	MOVE B,A
	MOVEM C,1(A)
	AOBJN A,.-1
	HRLI B,220600
	MOVEM B,(A)
	MOVN B,VLINEL
	SETCAM B,VDISCNT
	MOVEI B,A
	MOVEM B,VDISLP
	JRST FALSE

DISAD:	MOVEI R,DISAD2
	SKIPN B
	TLO R,-1
	PUSHJ P,DISHD
	HLRO B,A
	SUBI A,1(B)
	HRRM A,DISAD3
	MOVE A,C
	MOVEI AR1,TRUTH
	PUSHJ P,PRINTA
	JRST MAPL1

DISCR:	PUSH P,A
	PUSHJ P,DISAD5
	POP P,A

DISAD2:	JUMPE AR1,CPOPJ
	SOSG VDISCNT
	JRST DISCR
	CAILE A,132
	CAILE A,137
	JRST DISAD6
	MOVEI D,-133(A)
	MOVEI A,36
	PUSHJ P,DISAD4
	IMULI D,T
	MOVE A,[525446515053]
	ROT A,(D)
	PUSHJ P,DISAD4
	MOVEI A,35
DISAD6:	CAIN A,15
DISAD5:	MOVEI A,34
DISAD4:	HRRZ S,@DISAD3
	AOS S
	CAIL S,@DISAD3
	TDZA AR1,AR1
DISAD3:	IDPB A,DISAD3
	CAIE A,34
	POPJ P,
	MOVE S,VLINEL
	MOVEM S,VDISCNT
	AOS VDISLP
	SOJA A,DISAD2

TYOI:	0		;NELSON HAC TO OUTPUT
	PUSH P,.-1	;ONE CHARACTER BY <XCT "T, CTY>
	PUSH P,A
	MOVE A,TYOI
	LDB A,[270600,,-1(A)]
	PUSHJ P,(R)
	JRST POPAJ

PRINN:	HLRZ B,(A)	;PRINT FROM PNAME
	SETOM PCNT
PRINN1:	JUMPE B,CPOPJ
	HLRZ C,(B)
	HRRZ B,(B)
	HRLI C,440700
PRINN2:	ILDB A,C
	JUMPE A,PRINN1
	AOSN PCNT
	CAILE A,"9
	CAIGE A,"0
	CAIGE A,",
	CAIN A,177
	JUMPGE R,PRINN3
	CAIG A,")
	CAIGE A,"(
	CAIG A,40
	JUMPGE R,PRINN3
PRINN4:	PUSHJ P,(R)
	TLNN C,760000
	JRST PRINN1
	JRST PRINN2

PRINN3:	SKIPE PCNT
	CAIE A,"-
	XCT "/,CTY
	JRST PRINN4

TERPRI:	MOVEI A,15
CTYO:	PUSHJ P,TYO
	JRST FALSE

PCNT:	0

EQUAL:	MOVE C,P
EQUAL1:	CAMN A,B
	JRST,TRUE
	MOVE T,A
	MOVE TT,B
	PUSHJ P,ATOM
	EXCH A,B
	PUSHJ P,ATOM
	CAMN A,B
	JRST EQUAL3
EQUAL4:	MOVE P,C
	JRST,FALSE

EQUAL3:	JUMPN A,EQ2
	PUSH P,T
	PUSH P,TT
	HLRZ A,(T)
	HLRZ B,(TT)
	PUSHJ P,EQUAL1
	JUMPE A,EQUAL4
	POP P,B
	POP P,A
	HRRZ A,(A)
	HRRZ B,(B)
	JRST,EQUAL1

SUBST:	SKIPA AR1,A
SUBS0A:	SKIPA A,AR1
	SKIPA AR2A,B
	MOVE B,AR2A
	PUSH P,C
	MOVE A,C
	PUSHJ P,EQUAL
	POP P,C
	JUMPN A,MAPL1
SUBS1:	CAIG C,INUM
	JRST EV6A
	HLLE T,(C)
	AOJN T,SUBS2
EV6A:	MOVE A,C
	POPJ P,
SUBS2:	PUSH P,C
	HLRZ C,(C)
	PUSHJ P,SUBS0A
	EXCH A,(P)
	HRRZ C,(A)
	PUSHJ P,SUBS0A
SUBS3:	POP P,B
	JRST XCONS

NCONC:	TDZA R,R
APPEND:	MOVEI R,.APPEND-.NCONC
	JUMPE T,FALSE
	POP P,B
APP2:	AOJE T,PROG2
	POP P,A
	PUSHJ P,.NCONC(R)
	MOVE B,A
	JRST APP2
.APPEND:	JUMPE A,PROG2
	MOVEI C,AR1
	MOVE TT,A
APP1:	HLRZ A,(TT)
	PUSHJ P,CONS
	HRRM A,(C)
	MOVE C,A
	HRRZ TT,(TT)
	JUMPN TT,APP1
	JRST MAPL1

MEMBER:	CAIG A,INUM
	JRST MEMQ
	HLLE AR1,(A)
	AOJE AR1,MEMQ
	MOVE AR1,A
	MOVE AR2A,B
MEMB1:	JUMPE AR2A, FALSE
	MOVE A,AR1
	HLRZ B,(AR2A)
	PUSHJ P,EQUAL
	JUMPN A,CPOPJ
	HRRZ AR2A,(AR2A)
	JRST MEMB1

MEMQ1:	HRRZ B,(B)
MEMQ:	JUMPE B,FALSE
	HLRZ C,(B)
	CAMN A,C
	JRST TRUE
	JRST MEMQ1

FLATSIZE:	HLLZS FLAT1
	MOVEI R,FLAT2
	PUSHJ P,PRINTA
FLAT1:	MOVEI A,.
	JRST FIX1A
FLAT2:	AOS FLAT1
	POPJ P,

IOC:	MOVEI R,CNTROL
	JRST PRINTA

IOG:	JSP T,SPECBIND
	TTYOFF
	TAPRED
	TAPWRT
	PUSH P,B
	PUSHJ P,IOC
	POP P,A
	CALLF 0,(1)
	JRST UNBIND

EXPLODE:	SKIPA R,CEXPL1
%EXPLODE:	HRROI R,EXPL1
	MOVSI AR1,AR1
	PUSHJ P,PRINTA
	JRST MAPL1

EXPL1:	PUSH P,B
	PUSH P,C
	ANDI A,177
	CAIL A,"0
	CAILE A,"9
	JRST,EXPL2
	SUBI A,"0
	PUSHJ P,FIX1A
	JRST EXPL4

EXPL2:	LSH A,35
	PUSHJ P,FWCONS
	PUSHJ P,NCONS
	PUSH P,AR1
	PUSH P,TT
	PUSH P,T
	PUSHJ P,PNGNK1
	PUSHJ P,RINTERN
	POP P,T
	POP P,TT
	POP P,AR1
EXPL4:	PUSHJ P,NCONS
	HLR B,AR1
	HRRM A,(B)
	HRLM A,AR1
	POP P,C
	JRST POPBJ

READCH:	PUSHJ P,TYI
	MOVSI AR1,AR1
CEXPL1:	PUSHJ P,EXPL1
	HLRZ A,(AR1)
	POPJ P,

BOOLE:	MOVE TT,T
	ADDI TT,2(P)
	SOS A,-1(TT)
	DPB A,C1BOOL
	PUSHJ P,BOOLG
	MOVE C,A
BOOLL:	PUSHJ P,BOOLG
BOOLI:	SETZB C,A
	JRST,BOOLL
BOOLG:	CAIL TT,(P)
	JRST BOOL1
	MOVE A,(TT)
	PUSHJ P,NUMVAL
	AOJA TT,CPOPJ
BOOL1:	HRLI T,-1(T)
	ADD P,T
	POP P,B
	JRST FIX1A

C1BOOL:	350400,,BOOLI


MACDMP:	HRROI R,MACW2
	JUMPE T,MACD1
	POP P,A
	MOVE B,[(440700)MACSA]
	MOVEM B,MACCR
	MOVEM B,MACW2
	PUSHJ P,[AOJA R,PRINTA]
	MOVEI A,0
	IDPB A,MACW2
MACD1:	PUSHJ P,AGC
	MOVE A,FF
	MOVE B,F
	PUSHJ P,.NCONC
	PUSHJ P,FW0CNS
	JUMPN FF,.-1
	PUSHJ P,UWAIT"
	CONSZ TTY,30
	JRST .-2
.LOP SUB MACGO MACW2
	JRST .LVAL1(R)
;	JRST MACGO-MACW2(R)

MACW2:	0

MACW1:	IDPB A,MACW2
	POPJ P,

READLIST:	SKIPA T,READ+1
MAKNAM:	MOVEI T,CPOPJ
	JUMPE A,E7
	HRRM A,MKNAM3
	MOVEI A,MKNAM2
	PUSHJ P,READ0
	HRRZ T,MKNAM3
	CAIE T,-1
	JUMPN T,[LERR [SIXBIT \MORE THAN ONE S-EXPRESSION-MKNAM!\]]
	POPJ P,

MKNAM2:	PUSH P,B
	PUSH P,T
	PUSH P,TT
MKNAM3:	MOVEI TT,.
	JUMPE TT,MKNAM6
	CAIN TT,-1
	LERR [SIXBIT \READ UNHAPPY-MAKNAM!\]
	HRRZ B,(TT)
	HRRM B,MKNAM3
	HLRZ A,(TT)
	PUSHJ P,NUMBERP
	JUMPE A,MKNAM5
	HLRZ A,(TT)
	PUSHJ P,NUMVAL
	ADDI A,60
MKNAM4:	POP P,TT
	POP P,T
	JRST POPBJ
MKNAM5:	HLRZ A,(TT)
	MOVEI B,PNAME
	PUSHJ P,GET
	HLRZ A,(A)
	LDB A,C3MKN
	JRST MKNAM4

C3MKN:	(350700+A)

MKNAM6:	MOVEI A,40
	HLLOS MKNAM3
	JRST MKNAM4

AAND:	HRLI A,TRUTH
OR:	HLRZ C,A
	PUSH P,C
ANDOR:	HRRZ C,A
	JUMPE C,POPAJ
	MOVSI C,(SKIPE (P))
	TLNE A,-1
	MOVSI C,(SKIPN (P))
	XCT C
	JRST POPAJ
	MOVEM A,(P)
	HLRZ A,(A)
	PUSHJ P,EVAL
	EXCH A,(P)
	HRR A,(A)
	JRST ANDOR

GENSYM:	MOVEI B,GNUM
	HRLI B,10700
	MOVNI C,4
	MOVEI TT,"0

GENSY2:	LDB T,B
	AOS T
	DPB T,B
	CAIG T,"9
	JRST GENSY1
	DPB TT,B
	ADD B,C1GENS
	AOJN C,GENSY2
GENSY1:	MOVE A,GNUM
	PUSHJ P,FWCONS
	PUSHJ P,NCONS
	JRST PNGNK1

GNUM:	ASCII \G0000\	;INITIAL GENSYM
C1GENS:	070000,,0

PRINO:	MOVE A,(A)	;FLOATING POINT NUMBER
	SETZB B,C
	JUMPG A,FP1
	JUMPE A,FP3
	MOVNS,A
	XCT "-,CTY
FP1:	CAMGE A,FT01
	JRST,FP4
	CAML A,FT8
	AOJA B,FP4
FP3:	MULI A,400
	ASHC B,-243(A)
	MOVE A,B
	CLEARM FPTEM
	PUSHJ P,FP7
	XCT ".,CTY
	MOVNI T,10
	ADD T,FPTEM
	MOVE B,C
FP3A:	MOVE A,B
	MULI A,12
	PUSHJ P,FP7B
	SKIPE,B
	AOJL T,FP3A
CPOPJA:	POPJ P,


FP4:	MOVNI C,6
	MOVEI TT,0
FP4A:	ADDI TT,1(TT)
	XCT FCP(B)
	TRZA TT,1
	FMPR A,@FCP+1(B)
FP4B:	AOJN C,FP4A
	PUSH P,TT
	MOVNI B,-2(B)
	DPB B,[(300200)FP4C]
	PUSHJ P,FP3
	MOVEI A,"E
	PUSHJ P,(R)
FP4C:	XCT "+,CTY
	POP P,A
FP7:	JUMPE A,FP7A1
	IDIVI A,12
	AOS FPTEM
	HRLM B,(P)
	JUMPE A,FP7A1
	PUSHJ P,FP7
FP7A1:	HLRE A,(P)
FP7B:	ADDI A,60
	JRST (R)

	1.0^32.
	1.0^16.
FT8:	1.0^8
	1.0^4
	1.0^2
	1.0^1
FT:	1.0^0
	1.0^-32.
	1.0^-16.
	1.0^-8
	1.0^-4
	1.0^-2
FT01:	1.0^-1
FT0:
FCP:	CAMLE A,FT0(C)
	CAMGE A,FT(C)
	C,,FT0

FPTEM:	0

EV3:	HLRZ A,(AR1)
	MOVEI B,VALUE
	PUSHJ P,GET
	JUMPE A,QA2A	;FUNCTION OBJECT HAS NO DEFINITION
	HRRZ A,(A)
	CAIN A,UNBOUND
	JRST QA2A
	HRRZ B,(AR1)
	PUSHJ P,CONS
	JRST EVAL

OEVAL:	AOJN T,AEVAL
	POP P,A

EVAL:
EV0:	HRRZM A,AR1
	CAIG A,INUM
	JUMPN A,CPOPJ
	HLRZ T,(A)
	CAIN T,-1
	JRST EV5
	CAIG T,INUM
	JUMPN A,QA2A
	HLRO TT,(T)
	AOJE TT,EE2
	JRST EXP3


EV5:	HRRZ AR1,(AR1)
	JUMPE AR1,QA8
	HLRZ TT,(AR1)
	CAIE TT,FLONUM
	CAIN TT,FIXNUM
	POPJ P,
	HRRZ AR1,(AR1)
	CAIE TT,VALUE
	JRST EV5
	HLRZ AR1,(AR1)
	HRRZ AR1,(AR1)
	CAIN AR1,UNBOUND
	JRST QA8
	MOVEM AR1,A
	POPJ P,

ALIST:	SKIPE  A,-1(P)		;CREATE A PSEUDO-ALIST
	PUSHJ P,NUMBERP
	MOVEM SP,SPSV
	JUMPN A,AEVAL7
	MOVE C,SC2
	MOVEM C,AEVAL5
	SETOM AEVAL2
AEVAL8:	MOVE C,SP
AEVAL6:	CAMN C,AEVAL5
	JRST AEVAL1
	POP C,T
AEVAL4:	CAMN C,T
	JRST AEVAL6	;THRU WITH BLOCK
	POP C,AR1
	MOVSS AR1
	PUSH SP,(AR1)
	HLRZM AR1,(AR1)
	HRLM AR1,(SP)
	JRST AEVAL4

AEVAL5:	0

AEVAL:	PUSHJ P,ALIST	;EVAL WITH AN ALIST
	POP P,A
	MOVEI A,UNBIND
	EXCH A,(P)
	JRST EVAL

AEVAL1:	SKIPGE AEVAL2
	SKIPN B,-1(P)
	JRST ABIND3

ABIND1:	MOVEM SP,SPSV
ABIND2:	MOVE A,B
	HRRZ B,(A)
	HLRZ A,(A)
	HRRZ AR1,(A)
	HLRZ A,(A)
	PUSHJ P,BIND
	JUMPN B,ABIND2

ABIND3:	PUSH SP,SPSV
	POPJ P,

AEVAL7:	MOVE A,-1(P)
	PUSHJ P,NUMVAL
	CLEARM AEVAL2
	MOVEM A,AEVAL5
	JRST AEVAL8

AEVAL2:	0

EE2:	HRRZ T,(T)	;CAR (X) IS ATOMIC
	JUMPE T,EV3	;GET FUNCTION DEFINITION OFF ATOM
	HLRZ TT,(T)
	HRRZ T,(T)
	CAIN TT,SUBR
	JRST ESB
	CAIN TT,LSUBR
	JRST EELS
	CAIN TT,EXPR
	JRST AEXP
	CAIN TT,FSUBR
	JRST EFS
	CAIN TT,MACRO
	JRST EFM
	CAIE TT,FEXPR
	JRST EE2

EFX:	HLRZ T,(T)	;FOUND FEXPR
	HLL T,(AR1)
	PUSH P,T
	HRRZ A,(A)
	TLO A,400000	;SEE IAP4 FOR EXPLANATION OF THIS HAC
	PUSH P,A
	MOVNI T,1
	JRST IAPPLY

AEXP:	HLRZ T,(T)	;FOUND EXPR
	HLL T,(AR1)
EXP3:	PUSH P,T	;FOUND LAMBDA, LABEL, FUNARG
EXP1:	HRRZ A,(AR1)
CILIST:	JSP TT,ILIST
EXP2:	JRST IAPPLY

EFS:	HLRZ T,(T)	;FOUND FSUBR
	HRRZ A,(AR1)
	JRST (T)

ESB:	HRRZ A,(AR1)	;FOUND SUBR
UUOS2:	HLRZ T,(T)
	HLL T,(AR1)
	PUSH P,T
	JSP TT,ILIST
ESB1:	JRST .+NACS+1(T)		;POP OFF PDL AND JRST TO SUBR
	POP P,A+4
	POP P,A+3
	POP P,A+2
	POP P,A+1
POPAJ:	POP P,A
	POPJ P,

EFM:	HLRZ T,(T)	;FOUND MACRO
	CALLF 1,(T)
	JRST EVAL

APFNG1:	0

APPLY:	MOVEI TT,AP2
	CAME T,[-3]
	JRST PDLARG
	MOVEM T,APFNG1	;APPLY WITH AN ALIST
	PUSHJ P,ALIST
	MOVE T,APFNG1
	JSP TT,PDLARG
	PUSH P,CUNBIND
AP2:	PUSH P,A
	MOVEI T,0
AP3:	JUMPE B,IAPPLY
	HLRZ C,(B)
	PUSH P,C
	HRRZ B,(B)
	SOJA T,AP3

IAP4:	JUMPGE D,QF3	
	AOJN R,QF3
	PUSH P,B	;AT THIS POINT, WE MAKE UP AN
	MOVE A,SP	;ALIST FOR A FEXPR OF TWO LAMBDA VARS
	PUSHJ P,FIX1A
	EXCH A,(P)
	MOVE B,A
	MOVNI R,2
	SOJA T,IAP5

FUNCT:	PUSH P,A
	MOVE A,SP
	PUSHJ P,FIX1A
	POP P,B
	HLRZ B,(B)
	PUSHJ P,XCONS
	MOVEI B,FUNARG
	JRST XCONS

APFNG:	SOS T		;APPLY FUNARG
	MOVEM T,APFNG1
	JSP TT,PDLARG
	HRRZ A,(A)
	HRRZ D,(A)
	HLRZ A,(A)
	HRLZ R,APFNG1
	PUSH P,CUNBIND
	JSP TT,ARGP1
	PUSH P,D
	PUSHJ P,ALIST
	POP P,D
	AOS T,APFNG1

	;INTERNAL APPLY, FOR USE PARTICULARLY WITH "CALL" UUO'S

IAPPLY:	MOVE C,T	;STATE OF WORLD AT ENTRANCE
	ADDI C,(P)	;T HAS - NUMBER OF ARGS ON PDL
ILP1A:	HRRZ B,(C)	;NEXT PDL SLOT HAS FUNCTION- POSS FUN NAME IN LH
ILP1:	CAIG B,INUM
	JUMPN B,QA1
	HLRZ A,(B)
	CAIN A,-1
	JRST IAP1	;FN IS ATOMIC
	CAIN A,LAMBDA
	JRST IAPLMB
	CAIN A,FUNARG
	JRST APFNG
	CAIN A,LABEL
	JRST APLBL
	PUSH P,T
	MOVE A,B
	PUSHJ P,EVAL
	POP P,T
	MOVE C,T
	ADDI C,(P)
ILP1B:	MOVEM A,(C)
	JRST ILP1A

IAPXPR:	HLRZ A,(B)
	JRST ILP1B

IAP1:	HRRZ B,(B)
	JUMPE B,IAP2
	HLRZ TT,(B)
	HRRZ B,(B)
	CAIN TT,EXPR
	JRST IAPXPR
	CAIN TT,LSUBR
	JRST IAP6
	CAIE TT,SUBR
	JRST IAP1

IAPSBR:	HLRZ B,(B)
	MOVEM B,(C)
	JRST ESB1

IAPLMB:	HRRZ B,(B)
	HLRZ TT,(B)
	MOVEM SP,SPSV
	HRRZ B,(B)
	HLRZ D,(TT)
	CAIN D,-1
	JUMPN TT,IAP3
	MOVE R,T
IPLMB1:	JUMPE T,IPLMB2	;NO MORE ARGS
	JUMPE TT,QF2	;TOO MANY ARGS SUPPLIED
IAP5:	HLRZ A,(TT)
	MOVEI AR1,1(T)
	ADD AR1,P
	HLLZ D,(AR1)
	HRLM A,(AR1)
	HRRZ TT,(TT)
	AOJA T,IPLMB1

IPLMB2:	JUMPN TT,IAP4	;TOO FEW ARGS SUPPLIED
	JUMPE R,IAP69
IPLMB4:	POP P,AR1
	HLRZ A,AR1
	AOJG R,IPLMB3
	PUSHJ P,BIND	;BIND VALUES TO LAMBDA VARIABLES
	JRST IPLMB4
IPLMB3:	SKIPE BACTRF
	JRST APBK1
APBK2:	HLRZ A,(B)
	PUSH SP,SPSV
	PUSHJ P,EVAL

UNBIND:
SPECSTR:	POP SP,TT
UNBND1:	CAMN SP,TT
	POPJ P,
	POP SP,T
	MOVSS T
	HLRZM T,(T)
	JRST UNBND1

IAP69:	POP P,(P)
	HLRZ A,(B)
	JRST EVAL

APBK1:	HRRI AR1,CPOPJ 
	TLNE AR1,-1
	PUSH P,AR1
	JRST APBK2

IAP6:	MOVEI TT,CPOPJ
	MOVEM TT,(C)
	HLRZ B,(B)
	JRST (B)

APLBL:	MOVEM SP,SPSV	;APPLY LABEL EXPRESSION
	HRRZ B,(B)
	HLRZ A,(B)
	HRRZ B,(B)
	HLRZ AR1,(B)
	MOVEM AR1,(C)
	PUSHJ P,BIND
	MOVEI A,APLBL1
	EXCH A,-1(C)
	EXCH A,LBLAD
	HRLI A,LBLAD
	PUSH SP,A
	PUSH SP,SPSV
	JRST IAPPLY

APLBL1:	PUSH P,LBLAD
	JRST UNBIND

LBLAD:	0

IAP2:	HRRZ A,(C)	;APPLY FUNCTIONAL
	MOVEI B,VALUE
	PUSHJ P,GET
	JUMPE A,QA1
	HRRZ A,(A)
	CAIN A,UNBOUND
	JRST QA1
	JRST ILP1B

IAP3:	MOVNI AR1,-1(T)	;APPLY LEXPR
	MOVE A,TT
	PUSHJ P,BIND
	PUSH P,ARG
	SOS C
	HRRM C,ARG
	PUSH SP,SPSV
	HLRZ A,(B)
	PUSHJ P,EVAL
	HRRZ T,ARG
	POP P,ARG
	SUBI T,(P)
	HRLI T,-1(T)
	ADD P,T
CUNBIN:	JRST UNBIND

ARG:	HRRZ A,ARG(A)
	POPJ P,

LIST:	MOVE B,A
	MOVEI A,CEVAL
	JRST MAPCAR

EELS:	HLRZ TT,(T)	;FOUND LSUBR
	HRRZ A,(AR1)
ILIST:	MOVEI T,0
	JUMPE A,ILIST2
ILIST1:	PUSH P,A
	HLRZ A,(A)
	PUSH P,TT
	HRLM T,(P)
	PUSHJ P,EVAL
ILIST3:	POP P,TT
	HLRE T,TT
	EXCH A,(P)
	HRRZ A,(A)
	SOS T
	JUMPN A,ILIST1
ILIST2:	JRST (TT)

MAPC:	TLO A,400000
MAP:	TLOA A,200000
MAPCAR:	TLO A,400000
MAPLIST:	JUMPE B,FALSE
	PUSH P,A
	PUSH P,B
	PUSH P,B
	HRLZM P,(P)
MAPL2:	MOVE A,-1(P)
	SKIPGE -2(P)
	HLRZ A,(A)
	CALLF 1,@-2(P)
	LDB C,[(420100) -2(P)]
	JUMPN C,MAP1
	PUSHJ P,NCONS
	HLR B,(P)
	HRRM A,(B)
	HRLM A,(P)
MAP1:	HRRZ B,@-1(P)
	MOVEM B,-1(P)
	JUMPN B,MAPL2
	POP P,AR1
	SUB P,[(2)2]
MAPL1:	HRRZ A,AR1
	POPJ P,

READ:	MOVEI A,TYI
	MOVEI T,RINTERN
READ0:	HRRM A,READ2
	HRRM T,RDSP20
READ0A:	HRLZI T,2200
	JRST READ2
READ1:	HRLZI T,2000
READ2:	PUSHJ P,READ2
	PUSHJ P,RDOBJ
	CAIE B,40
	CAIN B,",
	JRST RDSPA
	CAIN B,"(
	JRST RDOPN
	CAIN B,")
	JRST RDCLOS
	CAIN B,".
	JRST RDDOT
	JRST RDDELE

RDSPA:	TLNE T,200
	POPJ P,
	JSP C,RDSPA3
	JRST READ2
RDSPA3:	PUSHJ P,NCONS
	TLNE T,2000
	JRST RDSPA1
	POP P,AR1
	HRRZ B,(AR1)
	HRRM B,(A)
	HRRM A,(AR1)
RDSPA2:	PUSH P,A
	MOVEI T,0
	JRST (C)
RDSPA1:	HRRM A,(A)
	JRST RDSPA2

RDOPN:	TLNE T,200
	JRST ER1
	JSP C,RDSPA3

RDPAR1:	HRRZI A,"(
	JRST READ2+1

RDDOT:	JSP C,RDSPA3
	HRLZI T,100
	JRST READ2

RDCLOS:	TLNE T,200
	JRST READ0A
	TLNN T,100
	PUSHJ P,NCONS
	TLNE T,2000
	POPJ P,
	POP P,AR1
	HRRZ B,(AR1)
	HRRM A,(AR1)
	JRST PROG2

RDOBJ:	SETZB B,C
	JRST RDCH+1

RDCH:	PUSHJ P,@READ2
	CAIG A,"Z
	CAIGE A,"A
	JRST RDOBJ1

RDLET1:	PUSHJ P,RDLET
	JRST RDCH
RDLET:	TLNE T,40
	JRST RDLETE
	TLZE T,10
	TLOA T,100
	JRST .+3
	TLNE T,2000
	JRST ER1
	TLON T,20
	JRST RDNEW
RDPAK:	SOJLE B,RDRLOD
	IDPB A,RDPTR
	POPJ P,

RDNEW:	HRRM A,RDRLD1
	PUSHJ P,FW0CNS
	HRLI A,700
	MOVEM A,RDPTR
	PUSHJ P,NCONS
	HRRM A,(A)
	JRST RDRLD2

RDRLOD:	HRRM A,RDRLD1
	HRRZ B,(AR1)
	PUSHJ P,FW0CNS
	HRRM A,RDPTR
	PUSHJ P,CONS
	HRRM A,(AR1)
RDRLD2:	SOS RDPTR
	MOVE AR1,A
	MOVEI B,AR2A
RDRLD1:	MOVEI A,0
	JRST RDPAK+1

RDLETE:	CAIE A,"E
	JRST ER1
	TLON T,5
	MOVEI TT,0
	MOVEI C,0
	POPJ P,

RDPTR:	0

RDOBJ1:	CAIG A,"9
	CAIGE A,"0
	JRST RDOBJ2

RDNUM:	TLNE T,20
	JRST RDLET1
	SUBI A,"0
	TLNE T,4
	JRST RDEXP
	TLO T,40
	TLZN T,12
	JRST RDNUM1
	TLO T,1
	MOVEI TT,0

RDNUM1:	IMULI B,12
	ADD B,A
	PUSH P,AR1
	MOVE R,VIBASE
	MULI C,-1(R)
	LSH AR1,1
	LSHC C,43
	POP P,AR1
	ADD C,A
	SOJA TT,RDCH

RDEXP:	IMULI C,12
	ADD C,A
	JRST RDCH

RDOBJ2:	CAIN A,11
	MOVEI A,",
	CAIE A,40
	CAIN A,",
	JRST RDSP
	CAIE A,"(
	JRST RDOBJ3

RDOP:	TLNE T,60
	JRST RDSP1
	TLZE T,10
	TLO T,100
	PUSH P,T
	PUSHJ P,READ1
	MOVEI B,",
	POP P,T
	JRST RDSPS+1

RDOBJ3:	CAIE A,")
	JRST RDOBJ4
RDCL:	TLNE T,60
	JRST RDSP1
	TLO T,100
	MOVEI B,")
	JRST FALSE

RDOBJ4:	CAIE A,".
	JRST RDOBJ6

RDDT:	TLNE T,40
	JRST RDDT1
	TLNE T,20
	JRST RDDT2
	TLOE T,10
	TLO T,100
	JRST RDCH

RDDT1:	TLOE T,2
	JRST RDSP1
	MOVEI TT,0
	JRST RDCH

RDDT2:	TLNE T,200
	JRST ER2
	JRST RDSP1

RDOBJ6:	CAIE A,"-
	JRST RDOBJ7
	TLNE T,20
	JRST RDLET1
	TLNE T,4
	TLOA T,1000
	TLO T,400
	JRST RDCH

RDSP:	TLZE T,10
	JRST RDSP2

RDSP1:	HRRM A,RDSPS
	TLNN T,20
	JRST RDSP3
	HRRZ A,(AR1)
	HLRM A,(AR1)
	PUSH P,T
	PUSHJ P,PNGNK1

RDSP20:	PUSHJ P,RDSP20
	POP P,T

RDSPS:	HRRZI B,0
	TLNE T,100
	JRST RDSP8
	POPJ P,

RDSP3:	TLNN T,40
	JRST RDCH
	TLNE T,1
	JRST RDSP4
	TLNE T,2
	MOVE C,B
	MOVE A,C
	MOVEI B,FIXNUM

RDSP5:	TLNE T,400
	MOVNS A
	PUSHJ P,MAKNUM
	JRST RDSPS

RDSP4:	TLNE T,1000
	MOVNS C
	TLNE T,4
	ADD TT,C
	TLNE TT,400000
	TLO T,1000
	MOVE A,B
	PUSHJ P,FLOAT
	MOVM B,TT
	JRST RDSP6

RDOBJ7:	CAIE A,177
	JRST RDOBJ8
RDRUB:	TLZE T,1577
	JRST RUBOUT
	MOVE B,A
	POPJ P,

RUBOUT:	PUSHJ P,@READ2
	JRST RDOBJ

RDSP7:	TLNE T,1000
	FDVR A,C1RD
	TLNN T,1000
	FMPR A,C1RD

RDSP6:	SOJGE B,RDSP7
	MOVEI B,FLONUM
	JRST RDSP5

RDSP2:	TLNE T,200
	JRST ER2
	TLO T,100
	JRST RDCH

RDSP8:	EXCH A,B
	CAIN A,")
	JRST RDSP9
	CAIE A,40
	CAIN A,",
	JRST .+4
	CAIN A,177
	JRST RDRUB
	JRST ER1
	PUSHJ P,@READ2
	JRST RDSP8+1

RDSP9:	EXCH A,B
	POPJ P,

C1RD:	MOVS D,NIL
RDOBJ8:	CAIE A,"/
	JRST RDOBJ9
	PUSHJ P,@READ2
	JRST RDLET1

RDDELE:	TLNE T,200
	JRST READ0A
	TLNN T,2000
	JRST RDDEL1
	POP P,T
	POP P,T
	JRST RUBOUT

RDDEL1:	POP P,AR1
	MOVE A,AR1
RDDEL3:	HRRZ B,(A)
	CAMN B,AR1
	JRST RDDEL2
	MOVE A,B
	JRST RDDEL3

RDDEL2:	CAMN A,AR1
	JRST READ1
	HRRZ B,(AR1)
	HRRM B,(A)
	PUSH P,A
	MOVEI T,0
	JRST READ2

RDOBJ9:	CAIL A,40
	JRST RDLET1
	JRST RDCH

RINTERN:	SETOM RINF
INTERN:	AOS RINF
	PUSH P,A
	MOVEI B,PNAME
	PUSHJ P,GET
	JUMPE A,E6
	HLRZ C,(A)
	LDB B,[(004300)(C)]
	IDIVI B,77
	HLRZ TT,OBTBL(B+1)
	HRRM B+1,MAKA2
	JRST MAKF+1

MAKF:	HRRZ TT,(TT)
	JUMPE TT,MAKA
	HLRZ AR1,(TT)
MAKF1:	HRRZ AR1,(AR1)
	JUMPE AR1,E6
	HLRZ T,(AR1)
	HRRZ AR1,(AR1)
	CAIE T,PNAME
	JRST MAKF1
	HRRZ T,A
	HLRZ AR1,(AR1)
MAK2:	JUMPE AR1,MAK1
	JUMPE T,MAKF
	HLRZ B,(AR1)
	MOVE B,(B)
	HLRZ C,(T)
	CAME B,(C)
	JRST MAKF
	HRRZ AR1,(AR1)
	HRRZ T,(T)
	JRST MAK2

MAKA:	POP P,A
MAKA2:	MOVEI C,MAKA2
	HLRZ B,OBTBL(C)
	PUSHJ P,CONS
	HRLM A,OBTBL(C)
	JRST CAR

MAK1:	JUMPN T,MAKF
	POP P,A
	SKIPN RINF
	PUSHJ P,RECLAIM
MAK1A:	HLRZ A,(TT)
	POPJ P,

RECLAIM:	HRRZS (A)
RECL1:	CAIGE A,@GCP
	CAIGE A,OBTBL
	POPJ P,
	CAIGE A,@GCP1
	JRST REFS

REFWS:	MOVEM FF,(A)
	MOVE FF,A
	POPJ P,

REFS:	PUSH P,A
	HLRZ A,(A)
	CAIN A,-1
	JRST POPAJ
	PUSHJ P,RECL1
	HRRZ A,@(P)
	PUSHJ P,RECL1
	MOVEM F,@(P)
	POP P,F
	POPJ P,

RINF:	0

%AMAKE:	PUSH P,A	;MAKE ALIST FOR FSUBR THAT REQUIRES IT
	MOVE A,SP
	PUSHJ P,FIX1A
	MOVE B,A
	JRST POPAJ

%UDT:	PUSHJ P,PRINT	;ERROR PRINT FOR UNDEFINED COMPUTED GO TAG
	STRT [SIXBIT \UNDEFINED COMPUTED GO TAG IN !\]
	HRRZ R,(P)
	PUSHJ P,ERRR2A
	JRST BACRTN

%LCALL:	MOVN A,T	;SET UP ROUTINE FOR COMPILE LSUBR
	PUSHJ P,FIX1A
	ADDI T,+1-1-1(P)
	PUSH P,T
	PUSHJ P,(3)
	POP P,T
	SUBI T,-1(P)
	HRLI T,-1(T)
	ADD P,T
	POPJ P,

ARRAY:	PUSH P,A
	MOVE A,VBPORG
	PUSHJ P,NUMVAL
	MOVEM A,BPPNR
	POP P,A
	HRRZ AR1,(A)
	HLRZ A,(A)
	HRRZ B,BPPNR
	AOS B
	MOVEI C,SUBR
	PUSHJ P,PUTPROP
	HLRZ A,(AR1)
	JUMPE A,ARRY1
	HRRZ A,BPPNR
	MOVE B,GCMKL
	PUSHJ P,CONS
	MOVEM A,GCMKL
ARRY1:	HRRZ A,(AR1)
	JSP TT,ILIST
	MOVE S,BPPNR
	MOVEI AR1,1
	MOVEI AR2A,1
	AOJGE T,ARRYS
	MOVEI D,A-1
	SUB D,T
ARRY2:	POP P,TT
	SOS TT
	IMULB TT,AR1
	ADDM AR1,AR2A
	TLC TT,221000
	DPB D,[(270400)TT]
	PUSH S,TT
	CAIN D,1
	JRST ARRY3
	MOVSI TT,(ADDI(1))(D)
	DPB D,[(270400)TT]
	PUSH S,TT
	SOJA D,ARRY2


ARRY3:	MOVN TT,AR2A
	TLCA TT,-(ADDI A,(B))
ARRYS:	HRLOI TT,+(ADDI A,)-1
ARRY3A:	ADDI TT,3(S)
	ADDI TT,3(S)
	PUSH S,TT
	PUSH S,[JRST ARYCOM]
	POP P,C
	IMULI AR1,-1(C)
	AOS AR1
	LSH AR1,-1
	MOVS AR2A,AR1
	MOVNS AR2A
	HRRI AR2A,1(S)
	MOVEM AR2A,@BPPNR
	ADDI AR1,1(S)
	MOVE A,AR1
	PUSHJ P,FIX1A
	MOVEM A,VBPORG
	POPJ P,

ARYCOM:	ROT A,-1
	SKIPGE B,A
	TLCA B,402200
	TLC B,222200
	LDB A,B
	POPJ P,

NSTORE:	TLO A,400000
STORE:	PUSH P,A
	PUSHJ P,CADR
	PUSHJ P,EVAL
	SKIPGE (P)
	PUSHJ P,NUMVAL
	EXCH A,(P)
	HLLM A,(P)
	HLRZ A,(A)
	PUSHJ P,EVAL
	POP P,A
	DPB A,B
	JUMPL A,FALSE
	POPJ P,

REMOB:	JUMPE A,FALSE
	PUSH P,A
	HLRZ A,(A)
	PUSHJ P,INTERN
	HLRZ B,@(P)
	CAME A,B
	JRST REMOB2
	HRRZ B,MAKA2
	HLRZ C,OBTBL(B)
	HLRZ T,(C)
	CAMN T,A
	JRST REMOB1
REMOB3:	MOVE TT,C
	HRRZ C,(C)
	HLRZ T,(C)
	CAME T,A
	JRST REMOB3
	HRRZ T,(C)
	HRRM T,(TT)
REMOB2:	POP P,A
	HRRZ A,(A)
	JRST REMOB

REMOB1:	HRRZ TT,(C)
	HRLM TT,OBTBL(B)
	JRST REMOB2

UNBOUND:	-1,,.+1
	(PNAME).+1
	(.+1)
	(UNBD1).+1
	(UNBD1+1)
UNBD1:	ASCII \UNBOUND\

GCMKL:	0
BPPNR:	0

EXAMINE:	PUSHJ P,NUMVAL
	MOVE A,(A)
	JRST MAKNUM

DEPOSIT:	PUSH P,B
	PUSHJ P,NUMVAL
	MOVE C,A
	POP P,A
	PUSHJ P,NUMVAL
	MOVEM A,(C)
	JRST FALSE

LSH:	PUSH P,B
	PUSHJ P,NUMVAL
	EXCH A,(P)
	PUSHJ P,NUMVAL
	POP P,B
	LSH B,(A)
	JRST FIX1

SPECBIND:	MOVE TT,SP
SPEC1:	LDB R,[271500,,(T)]
	CAILE R,17
	JRST SPECX
	SKIPE R
	MOVE R,(R)
	EXCH R,@(T)
	HRL R,(T)
	PUSH SP,R
	AOJA T,SPEC1

SPECX:	PUSH SP,TT
	JRST (T)

INUM=REMOB2			;;;XXX ????

VARIABLES
CONSTANTS

PATCH:	BLOCK 20


;DEFINED ATOMS AND INITIAL LIST STRUCTURE

FS:
DEFINE MAKBUC A\B
DEFINE OT!A
EXPUNGE B		;;; XXX HACK AROUND MIDAS BUG, WHERE RELOCATION DOESN'T CHANGE
B=.
TERMIN
(B)IFN OBTSIZ-1-A,.+1
IF1 B=0
TERMIN

OBTBL:
OBLIST:	REPEAT OBTSIZ,MAKBUC \.RPCNT

DEFINE ADDOB A,C\B
OT!A
DEFINE OT!A
EXPUNGE B		;;; XXX HACK AROUND MIDAS BUG, WHERE RELOCATION DOESN'T CHANGE
B=.
TERMIN
IF1 B=0
(C)B
TERMIN

DEFINE PUTOB A,B
ZZ=<(377777),-1>&ASCII \A\
ZZ=-ZZ/OBTSIZ*OBTSIZ+ZZ
ADDOB \ZZ,B
TERMIN

DEFINE PSTRCT A
.ZZ=[ASCII \A\]			;XXX CAN'T USE ZZ HERE BECAUSE SYMBOL WILL STAY ABSOLUTE FROM PUTOB
ZY=.LENGTH \A\
(.ZZ)!REPEAT <ZY-1>/5,[.+1
(.ZZ+.RPCNT+1)]
TERMIN

DEFINE MKAT A,B,C,D
PUTOB A,.+1
D(,-1).+1
(B).+1
(,C!A).+1
(PNAME).+1
(.+1)
PSTRCT A
TERMIN

DEFINE MKAT1 A,B,C,D,E
	PUTOB E!C,.+1
	-1,,.+1
	B,,.+1
	D!A,,.+1
	PNAME,,.+1
	.+1,,
	PSTRCT E!C
TERMIN

IRPS A,,[RPLACA,RPLACD,MINUS,TERPRI,READ,CAR,CDR,CAAR,
CADR,CDAR,CDDR,CAAAR,CAADR,CADAR,CADDR,CDAAR,CDADR,CDDAR,CDDDR,
CAAAAR,CAAADR,CAADAR,CAADDR,CADAAR,CADADR,CADDAR,CADDDR,CDAAAR,
CDAADR,CDADAR,CDADDR,CDDAAR,CDDADR,CDDDAR,CDDDDR,MAKNUM,CONS,
ATOM,EQ,PRIN1,PRINT,RETURN,EXPLODE,SASSOC,ASSOC,
NUMBERP,EQUAL,SUBST,GET,INTERN,MEMBER,
MAKNAM,READCH,NOT,GENSYM,ZEROP,FORMF,FIX,SET,PROG2,LENGTH,READLIST,LAST,ADD1,SUB1,
REVERSE,SPEAK,MAPLIST,GC,GETL,BAKGAG,MEMQ,PUTPROP,PRINC,FLATSIZE,ERR,MAPCAR,
EXAMINE,DEPOSIT,LSH,IOG,NCONS,XCONS,REMPROP,ARG,NOUUO,MINUSP,MAP,MAPC,REMAINDER,ABS,
VIDI,TIME,SETTIME,DISAD,DISINI,LPEN,VIDLIN,VIDLOG]
MKAT A,SUBR
TERMIN
MKAT EXPLODEC,SUBR,%
MKAT TYO,SUBR,I
CEVAL=.+1
MKAT1 EVAL,SUBR,*EVAL

IRPS A,,[LIST,COND,PROG,SETQ,UREAD,ERRSET,REMOB
	OR,GO,ARRAY,STORE,NSTORE,DEFPROP,IOC,UFILE]
	MKAT A,FSUBR
TERMIN
MKAT1 QUOTE,FSUBR,FUNCTION
MKAT1 FUNCT,FSUBR,*FUNCTION

IRPS A,,[PLUS TIMES DIFFERENCE QUOTIENT APPEND NCONC
	BOOLE APPLY UWRITE GREATERP LESSP UKILL MACDMP]
	MKAT A,LSUBR
TERMIN
MKAT1 OEVAL,LSUBR,EVAL

MKAT QUOTE,FSUBR,,CQUOTE:

	PUTOB T,.+1
TRUTH:	-1,,.+1
	(VALUE).+1
	(VTRUTH).+1
	(PNAME).+1
	(.+1)
	PSTRCT T
VTRUTH:	TRUTH

	PUTOB NIL,0
CNIL2:	(VALUE).+1
	(VNIL).+1
	(PNAME).+1
	(.+1)
	PSTRCT NIL
VNIL:	NIL

IRPS A,,[LCALL,AMAKE,UDT]
MKAT1 A,SYM,A,1+%,*
TERMIN

IRP A,,[PLUS,DIF,QUO,TIMES,APPEND,RSET,GREAT,LESS]
MKAT1 A,SUBR,A,.,*
TERMIN

IRPS A,,[SPECBIND,SPECSTR,NUMVAL,FIX1A]
V!A:	(,-1).+1
	(FIXNUM)[A]
MKAT A,SYM,V
TERMIN

IRPS A,,[OBLIST,BASE,IBASE,LINEL,CHRCT,BPEND,BPORG,DISLIST,DISCNT,DISLP]
	MKAT A,VALUE,V
TERMIN

VLINEL:	72.+1
VCHRCT:	71.+1
VOBLIST:	OBLIST
VBASE:	8+1
VIBASE:	8+1
VDISLIST: 0
VDISCNT: 72.
VDISLP:	1

IRP A,,[^Q,^W,^R,^C,^B,*NOPOINT]B,,[TAPRED,TTYOFF,TAPWRT,GCGAGV,LPTON,V.NOPOINT]
	MKAT1 B,VALUE,A
	B:	NIL
TERMIN

IRPS A,,[PNAME,FIXNUM,FLONUM,VALUE,LAMBDA,SUBR,FSUBR,EXPR,FEXPR,SYM,
	LABEL,FUNARG,LSUBR,MACRO]
	PUTOB A,.+1
A:	(,-1).+1
	(PNAME).+1
	(.+1)
	PSTRCT A
TERMIN

MKAT NULL,SUBR,A
MKAT AND,FSUBR,A

IRP A,,[BPORG,BPEND]B,,[VBP1,VBPE1]
V!A:	.+1
	(,-1).+1
	(FIXNUM)B
TERMIN

BFWS:		;ASSEMBLED FULL WORDS GO HERE

CONSTANTS
VBP1:	0	;INITIAL ALLOCATED VALUE FOR BPORG
VBPE1:	0	;INITIAL ALLOCATED VALUE FOR BPEND
EFWS:	0


			;LISP STORAGE ALLOCATOR

ALLTYO:	HRLOI A,700+A
	HLLM A,(P)
	ILDB C,(P)
	JUMPE C,ALLPOP
	PUSHJ P,ALLTYC
	JRST .-3

ALLTYI:	CONSO TTY,40
	JRST .-1
	DATAI TTY,C
	ANDI C,177
ALLTYC:	CONSZ TTY,20
	JRST .-1
	DATAO TTY,C
ALLPOP:	POPJ P,

ALLNUM:			;GETS A NUMBER FOR SOME STORAGE AREA SIZE
	MOVSI A,400000	;GET VALUE FROM TTY
	PUSHJ P,ALLTYI
	CAIN C,177
	JRST ALLRUB
	CAIL C,60
	CAIL C,72
	POPJ P,
	TLZ A,400000
	IMULI A,10
	ADDI A,-60(C)
	JRST ALLNUM+1

ALLPDL: 	BLOCK 10	;A "PDL" FOR ALLOC

ALLRUB:	PUSHJ P,ALLTYO
	ASCII /X/
	JRST ALLNUM

ALLOC":	CONO 200000
	MOVEI P,ALLPDL-1
	PUSHJ P,ALLTYO
	ASCII \
ALLOC? \
	PUSHJ P,ALLTYI
	CAIGE C,"O
	JRST ALLC00
	PUSHJ P,ALLTYO
	ASCII \
MEMTOP=\
	PUSHJ P,ALLNUM
	SKIPG A
	MOVEI A,37170+BSUP
	HRRM A,ALLC01
	MOVE B,[JRST TYIN+3]
	CAILE A,37777
	MOVEM B,TYIN

	PUSHJ P,ALLTYO
	ASCII \
FULL WDS=\
	PUSHJ P,ALLNUM
	SKIPGE A
	MOVEI A,400
	HRRM A,ALLC02

	PUSHJ P,ALLTYO
	ASCII \
BIN.PROG.SP=\
	PUSHJ P,ALLNUM
	SKIPGE A
	MOVEI A,1000
	HRRM A,ALLC10

	PUSHJ P,ALLTYO
	ASCII \
SPEC.PDL=\
	PUSHJ P,ALLNUM
	SKIPGE A
	MOVEI A,1000
	HRRM A,ALLC20
	MOVNS A
	HRRM A,ALLC21

	PUSHJ P,ALLTYO
	ASCII \
REG. PDL=\
	PUSHJ P,ALLNUM
	SKIPGE A
	MOVEI A,1000
	HRRM A,ALLC30

ALLC00:	MOVEI A,LISPGO
	HRRM A,100
	PUSHJ P,ALLTYO
	ASCII \
\
ALLC01:	MOVEI A,37170+BSUP
	HRRM A,BPSH
	MOVEM A,VBPE1
ALLC10:	SUBI A,1000
	HRRM A,BPSL
	MOVEM A,VBP1
ALLC20:	SUBI A,1000
ALLC21:	HRLI A,-1000
	MOVEM A,SC2
	SUBI A,FS
	HRRZS B,A
	ASH A,-4
ALLC02:	SUBI A,0
	MOVE C,B
	ASH C,-6
ALLC30:	ADDI C,1000
	MOVEI T,36.	;BIT TABLE FOR FWS SPACE
	IDIVM A,T
	AOS T		;SIZE OF BTF
	SUB B,T
	SUB B,A
	SUB B,C
	MOVEI TT,32.+1
	IDIVM B,TT	;BT SIZE -1
	SUBI B,1(TT)	;FREE STORAGE SIZE
	HRRZ AR1,B
	ADDI B,FS
	HRRM B,GCP1
	MOVN SP,B
	HRRM SP,GCMFWS
	HRLZM A,C1GCS	;LENGTH OF FWS
	MOVNS C1GCS
	HRRM B,C1GCS
	ADDI B,-1(A)

ALLOC1:	AOS B
	MOVEI SP,FS
	LSH SP,-5
	SUBM B,SP
	HRRM SP,GCBTP2
	HRRM SP,GCBTP1
	HRLM B,C3GC
	HRRM B,GCP2
	HRRM B,GCP
	MOVNI SP,-1(TT)
	HRLM SP,C3GCS
	HRRM B,C3GCS
	AOS B
ALLC2A:	MOVEI SP,FS
	ANDI SP,37
	HRRM SP,GCBTL2
	SUBI SP,32.
	HRRM SP,GCBTL1
	HRRM B,C3GC
	ADDI B,-1(TT)	;BOTTOM OF BTF-1
	HRRM B,C2GCS
	AOS B
	HRRM B,C2GC
	ADDI B,-1(T)	
	HRRM B,GCP5	;BTF TOP
	AOS B
	MOVEI A,FS
	MOVEM A,(B)
	HRRM B,GCP3
	AOS B
	HRRM B,GCSP1
	HRRM B,GCP4
	ADDI B,10
	HRRM B,GCP4+1
	AOS B
	HRRM B,C2
	MOVNI A,-20(C)
	HRLM A,C2

RLOCA:	MOVE B,AR1
	HRLI AR1,BFWS
	HRRI AR1,FS(B)
	HRRZI AR2A,EFWS-BFWS(AR1)
	BLT AR1,(AR2A)
	MOVEI AR1,FS-BFWS(B)
	MOVEI AR2A,FS
REL1:	HLRZ A,(AR2A)
	CAIG A,EFWS
	CAIGE A,BFWS
	JRST REL2
	ADD A,AR1
	HRLM A,(AR2A)
REL2:	HRRZ A,(AR2A)
	CAIG A,EFWS
	CAIGE A,BFWS
	JRST REL3
	ADD A,AR1
	HRRM A,(AR2A)
REL3:	CAIGE AR2A,BFWS-1
	AOJA AR2A,REL1
	CONSZ TTY,20
	JRST .-1
	JRST LISPGO

CONSTANTS

END

