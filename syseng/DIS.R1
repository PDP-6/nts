TITLE DISPLAY
;RECONSTRUCTED FROM @ MIDAS AND RG; C 524

B=5

.ZZ==.
LOC 40+2*DISCHN"
	BLKO DIS,BLKOP
	JRST 1,.
LOC .ZZ

.RECYC":	CONSO DIS,7400
	JRST POPRET"
	EXCH A",BLKOPR
	MOVEM A,BLKOP
	EXCH A,BLKOPR
	CONO DIS,100+FLGCHN"*10+DISCHN"
	JRST POPRET"

BLKOP":	DISBUF-1
BLKOPR:	DISBUF-1
DISBUF:	BLOCK DISIZE"
CHSIZ":	20
CHCNTS:	0
CHCNT:	0
DISPNR":	0


DISINI":	PUSH P",A
	PUSH P,B
	CONO DIS,100
	MOVEI A,DISBUF-1
	MOVEI B,20117
	IOR B,CHSIZ
	PUSH A,B
	PUSH A,[(221700)060000]
	HRLI A,600
	MOVEM A,DISPNR
	LDB B,[(40200)CHSIZ]
	MOVNS B
	MOVEI A,170.
	LSH A,(B)
	TRZ A,7
	MOVEM A,CHCNTS
	MOVNM A,CHCNT
	MOVEI A,40.
	MOVEM A,LINES
	JRST CPBA

DISFUL:	PUSH P,A
	PUSHJ P,DISCLG
	PUSHJ P,TYI
	PUSHJ P,DISINI
	POP P,A

DISAD":	SKIPN LINES
	JRST DISFUL
	PUSH P,A
DISAD7:	CAIL A,40
	CAILE A,132
	JRST DISAD1
DISAD5:	AOSG CHCNT
DISAD6:	IDPB A,DISPNR
	HRRZ A,DISPNR
	CAILE A,DISBUF+DISIZE-3
	CLEARM LINES
POPAJ:	POP P,A
	MOVEM A,DISLCR
	POPJ P,
DISAD1:	CAIN A,15
	JRST DISAD2
	CAIN A,12
	JRST DISAD3
	CAIN A,7
	JRST DISAD8
	CAIE A,11
	JRST DISAD4
	MOVEI A,40
	PUSHJ P,DSAD5A
	LDB A,[(300)CHCNT]
	JUMPN A,.-3
	JRST POPAJ
DISAD2:	MOVE A,CHCNTS
	MOVNM A,CHCNT
	MOVEI A,34
	PUSHJ P,DSAD6A
	JRST DSD3A
DISAD4:	CAIL A,40
	CAILE A,140
	JRST DISBLB
	MOVS A,BLOBS-133(A)
DISAD9:	HRRI A,36
	IDPB A,DISPNR
	MOVSS A
	IDPB A,DISPNR
	MOVEI A,35
	JRST DISAD5

DISAD3:	EXCH A,DISLCR
	CAIE A,15
	CAMN A,DISLCR
	JRST POPAJ
DSD3A:	SOS LINES
	MOVEI A,33
	JRST DISAD6

DISAD8:	MOVSI A,63
	JRST DISAD9


BLOBS:	53
	52
	54
	46
	51
	50
LINES:	0


DSAD5A:	PUSH P,A
	JRST DISAD5

DSAD6A:	PUSH P,A
	JRST DISAD6

DISBLB:	PUSH P,A
	MOVEI A,36
	IDPB A,DISPNR
	MOVEI A,50
	IDPB A,DISPNR
	MOVEI A,73
	IDPB A,DISPNR
	MOVEI A,35
	IDPB A,DISPNR
	MOVE A,(P)
	CAIGE A,100
	JRST DISBB1
	MOVEI A,61
	PUSHJ P,DSAD5A

DISBB1:	MOVE A,(P)
	ANDI A,70
	JUMPE A,DISBB2
	LSH A,-3
	ADDI A,60
	PUSHJ P,DSAD5A

DISBB2:	POP P,A
	ANDI A,7
	ADDI A,60
	PUSHJ P,DSAD5A
	MOVSI A,77
	JRST DISAD9

DISCLG:	PUSH P,A
	PUSH P,B
	MOVE A,DISPNR
	MOVEI B,37
	IDPB B,A
	TLNE A,550000
	JRST .-2
	TLC A,600#2200
	MOVEI B,3000
	IDPB B,A
	HRRZS A
	MOVEI B,3
	CAIGE A,DISBUF+24	;;WHAT IS 24?
	XORM B,DISBUF
	CONO PI,2200+200_<-FLGCHN>+200_<-DISCHN>
	CONO DIS,100+FLGCHN*10
	DATAO DIS,[3000]
CPBA:	POP P,B
	JRST POPAJ

DISLCR:	0

CONSTANTS
END
