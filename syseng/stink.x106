TITLE STINKING ODOR

;LINKING LOADER ONE/TWO PASS TAPES TMRC VERSION 4

MOBY==0		;MEM SIZE -40000

P=1		;PDL PNTR
A=2		;RANDOM
B=3		;"
C=4		;" (ALSO AC FOR .LOP)
D=5		;FREQ PNTR TO CUR LOC FOR SYMTAB HACKING (ALSO MEM FOR .LOP)
T=6		;FREQ HAS CUR WD
TT=7		;FREQ HAS CUR CODE BITS
ADR=10		;ADR LOADING INTO
BOT=11		;PNTR TO BOT LOADER SYMTAB
CKS=12		;CKSM CUR BLK
LL=13		;ADR FOR DISP WHEN BLK COMPLETE
RH=14		;GLOBAL SUM ETC. TO ADD TO RH OF CUR WD (W/O CARRY TO LH)
FF=17		;RANDOM FLAGS - SEE BELOW

TPCHN==1	;DC CHNL
TPCHNA==100	;BIT IN PI CONO FOR TPCHN
PCHCHN==2	;UTC CHNL
APRCHN==3	;CLOCK ETC.
LOW==40+APRCHN*2+1	;LOWEST LOC. CAN BE REALLY LOADED W/O PI LOSING
LOWLOD==41	;LOWEST LOCATION LOADED (GOES INTO DISPLACED AREA IF LOWLOD<=ADR<LOW)
LPDL==20	;PDL SIZE

MACDMP=37400+MOBY	;FOR DING & D
NURBUF==3		;# UT BLKS TO BUF
ICOMM==10000+MOBY	;INIT COMMON ORG
NUMSYM==112		;INITIAL DDT SYMBOLS
COMPTR=37777+MOBY	;MACDMP COMMAND POINTER
DDTSYM=33776+MOBY	;END +1 OF DDT SYMTAB FOR INIT AOBJN PNTR

;RIGHT HALF FLAGS
ALTF==1		;ALT MODE SEEN ON TYPEIN
LINF==2		;TYPE LF ON TYI
ARG==4		;ARG TYPED BEFORE CMND
UNDEF==10	;COMPLAIN ABOUT UNDEF
INDEF==20	;GLOBAL LOC
GLOSYM==40	;ENTER GLOBAL SYMS INTO DDT TABLE
SEARCH==100	;LIBRARY
CODEF==200	;SPECIAL WORD LOADED
GPARAM==400	;ENTER GPA LOCALS
COND==1000	;LOAD TIME CONDITIONAL
LOCAL==2000	;LOCAL SYM SEEN IN TYPE-IN

;LEFT HALF
LSTP==1		;1 MEANS TAPE STOPPED
EOF==2		;LAST BLOCK IN FILE SEEN
FORW==4		;1 MEANS FORWARD
SDLY==10	;TAPE STOPPING


LOC 34000-2+MOBY	;I.E. DDT-2

IF2,COMLOD=TPOK	;IS YOUR TAPE OK? (COMMON DOESN'T REALLY WIN)


DEFINE TYPE A
IFE A&40,[JSR A,TYP]		;JSR TO TYP IF NO 40 BIT NEEDED (THAT BIT OFF IN JSR OPCODE)
IFN A&40,[XCT A,CRL]		;ELSE USE XCT - TYP WILL SEE 40 BIT FROM XCT OPCODE
IFN A&120,[PRINTX /A -- TYPE
/]				;BARF IF CHAR LOSES
TERMIN
DEFINE INFORM A,B		;USE MACRO SO \ WORKS
IF1,[PRINTX / A = B
/]
TERMIN

INFORM [DDT INITIAL SYMS]\NUMSYM

DDPTR:	DDTSYM		;INIT SYMTAB PNTR FOR DDT
DDTSW:	(,-1).-1	;DDT SYMTAB PNTR PNTR
	JSR EXCHN	;SWAP TRAP LOCS
	JRST LIS	;GO TO CMND LUP
DD1:	BLT 16,MACDMP-200-2-1	;CLEAR LOADER FOR MERGE LOAD
	JRST MACDMP+1	;GO TO MACDMP & XCT CMNDS
REL:	ADDI@ T,FACTOR	;ADD APPROP RELOC (PROG MOD FOR REL OR GLOB OFF)
ABS:	HRRZ ADR,T	;SET LOAD ADR
DATABK:	HRRZS ADR	;CLEAR LH OF ADR (MAY HAVE CRUFT FROM GLOBAL RQ)
	PUSHJ P,GETBIT	;GET CODE BITS
	TRZE TT,4	;IF SPEC TYPE
	JRST DATBK1	;PROC GLOB, LINK RQ ETC.
	PUSHJ P,RRELOC	;GET WD & RELOC ACC TO CODE
COM1:	ADDB T,AWORD	;ADD FW FACTOR
	ADD T,RH	;ADD HW FACTOR
	HLL T,AWORD	;FLUSH CARRY INTO LH
	CLEARB RH,AWORD	;CLEAR BOTH
	CAIGE ADR,LOWLOD	;IF BELOW LOADED LOCS
	AOJA ADR,DATABK	;DON'T LOAD
	CAIGE ADR,LOW	;IF IN SHUFFLED TRAP LOCS
	MOVEM T,DISPL(ADR)	;LOAD INTO DISPLACED LOCS
	CAIL ADR,LOW	;OTHERWISE
	MOVEM T,(ADR)	;JUST LOAD
	TRNE FF,CODEF	;IF SPECIAL WD
	AOJA ADR,DATABK	;PRETEND NOT REALLY LOADED
	CAMLE ADR,HIGHCD	;ELSE CHECK HIGHEST LOADED LOC
	HRRZM ADR,HIGHCD	;UPDATE IF EXCEEDED
	HRRZ A,BOT	;GET CUR TBL ADR
	CAMLE A,HIGHCD	;IF NO CLASH WITH CODE
	AOJA ADR,DATABK	;KEEP GOING
ERR1:	(6000+SIXBIT /SCE/)	;ELSE BARF ABOUT NO ROOM
	JRST LI3	;& GET NEW CMND

DATBK1:	PUSHJ P,RLKUP	;GLOBAL, LINK, OR SPEC - GET SYM & LOOK UP
	TRNE TT,2	;IF LINK OR SPEC
	JRST DECODE	;DISPATCH FURTHER
USE:	ROTC T,3	;ELSE GET CODE BITS FROM SQUOZE
	HRL ADR,TT	;PUT IN ADR TO MAKE REF PNTR
	SKIPE C,TIMES	;GET
	CLEARM TIMES	;& CLEAR MPY FACTOR
	DPB C,[(261200)ADR]	;PUT INTO REF WD FOR UNDEF
	JUMPGE D,USE1A	;IF NOT SEEN YET ENTER REF OR BARF IF NOT ALLOWED
	TLNE B,200000	;IF ALREADY DEFINED
	JRST USE2	;FILL RQ
	TRNE FF,UNDEF	;ELSE IF UNDEF NOT OK
	JRST ERR2	;BARF
	PUSHJ P,DOWN	;OTHERWISE MOVE DOWN PART OF TBL BELOW SYM
	MOVEM ADR,(D)	;ADD REF (ADR OF RQ + RANDOM CRUFT)
CDATABK:	JRST DATABK	;& CONTINUE PROC WDS

USE2:	MOVE T,1(D)	;FILL REQUEST
	PUSHJ P,DECGEN
	ADDM T,AWORD
	ADDM TT,RH
	JRST DATABK

USE1A:	MOVE T,ADR
USE1:	TLO A,400000
	TRNN FF,UNDEF
	JRST DEF1A	;ENTER DEF
ERR2:	(5000+SIXBIT /UGA/)
	JRST DATABK


DEF1:	TLO A,600000
	TRNN FF,INDEF+GPARAM	;DEFINE ALL SYMBOLS
	TLNE A,40000	;OTHERWISE, FLUSH LOCALS
	JRST ENT
	JRST DEF4

RDEF:	TRO TT,10	;SET FLAG FOR REDEFINITION
DEF:	ROTC T,3
	PUSHJ P,RRELOC
DFSYM1:	PUSH P,CDATABK
DEFSYM:	MOVEM T,T1
DFSYM2:	JUMPGE D,DEF1	;NOT PREV SEEN
	TLNN B,200000	;PREVIOUSLY DEFINED
	JRST PATCH5	;PREVIOUSLY NEEDED

DEF2:	TRNE TT,100	;REDEFINE NOT OK
DEF3:	MOVEM T,1(D)
	CAME T,1(D)
	(5000+SIXBIT /MDG/)
DEF4:	TRZ FF,GPARAM
	POPJ P,

PATCH3:	PUSH P,PATCH6
PATCH:	HRRZ C,D
	SUB C,T2
	HRLS C
	HRRM C,PATCH4
	EXCH BOT,C
	ADD BOT,C
	MOVNI C,(C)
	ADD C,T2
	MOVSS C
	HRR C,T2
	SOS C
PATCH4:	POP C,.(C)
	JUMPGE C,.-1
PATCH6:	POPJ P,.+1

PATCH7:	PUSHJ P,LKUP1A
	JUMPGE D,DEF1
PATCH5:	HRRZM D,T2

PATCH1:	MOVE T,T1
	AOBJP D,PATCH3
	SKIPGE B,(D)
	JRST PATCH3	;CLEAN UP TABLE
	HLL ADR,B
	HRRZS B
	CAIGE B,LOWLOD
	JRST PATCH1
	TLZN ADR,100000
	JRST GEN	;GENERAL REQUEST
	PUSH P,CPTCH1
UNTHR:	CAIGE B,LOW
	ADDI B,DISPL
	HRL T,(B)
	HRRM T,(B)
	HLRZ B,T
	JUMPN B,UNTHR
CPTCH1:	POPJ P,PATCH1


GEN:	CAIGE B,LOW
	ADDI B,DISPL
	PUSHJ P,DECGEN
	ADD T,(B)
	ADD TT,T
	HRR T,TT
	MOVEM T,(B)
	JRST PATCH1

DECGEN:	MOVEI TT,0
	TLNE ADR,10
	MOVNS T
	LDB C,[(261200)ADR]
	SKIPE C
	IMUL T,C
	LDB C,[(220200)ADR]
	TLNE ADR,4
	MOVSS T
	XCT WRDTAB(C)

WRDTAB:	POPJ P,		;FW
	EXCH T,TT	;RH
	HLLZS T		;LH
	ROT T,5		;AC


DECODE:	TRNN TT,1
	JRST THRDR	;6 > LINK REQ
	PUSHJ P,GETBIT
	JRST @.+1(TT)
	DEF	;DEFINE SYMBOL (70)
	COMMON	;COMMON RELOCATION (71)
	LOCGLO	;LOCAL TO GLOBAL RECOVERY (72)
	LIBREQ	;LIBRARY REQUEST (73)
	RDEF	;REDEFINITION (74)
	REPT	;GLOBAL MULTIPLIED BY 1024>N>0 (75)
	DEFPT	;DEFINE AS POINT (76)



RLKUP:	PUSHJ P,RPB

LKUP:	MOVE A,T
LKUP1B:	MOVE D,BOT
LKUP3:	MOVEI B,0(ADR)	;CONTAINS GLOBAL OFFSET
	TRNN FF,CODEF
	MOVEM B,CPOINT+1
	TLZA A,700000
LKUP7:	MOVE D,[(,EISYM-EISYME)EISYM]
LKUP1A:	MOVE B,(D)
LKUP1:	TLZ B,600000
	CAMN B,A
	JRST LKUP5
	AOBJP D,LKUP6
	AOBJP D,LKUP6
	SKIPGE B,(D)
	JRST LKUP1
	JRST .-3

LKUP6:	CAIE D,EISYME
	JRST LKUP7
LKUP5:	MOVE B,(D)
	POPJ P,

RRELOC:	PUSHJ P,RPB
RELOC:	HLRZ C,T
	TRNE TT,1
	ADD T,FACTOR
	TRNE TT,2
	ADD C,FACTOR
	HRL T,C
	POPJ P,

DOWN:	HRL C,BOT
	SUB BOT,[(1)1]
	HRR C,BOT
	BLT C,-1(D)
	JRST ENT1

DEF1A:	PUSH P,CDATABK

ENT:	SUB BOT,[(2)2]
	MOVEM A,(BOT)
	MOVEM T,1(BOT)
ENT1:	HRRZ C,BOT
	TRNN FF,CODEF
	CAMLE C,HIGHCD
	JRST SYMS7
	JRST ERR1
THRDR:	PUSHJ P,RPB
	TLNE T,100000
	ADD T,FACTOR
	HRLI T,100000
	JUMPGE D,USE1
	MOVE B,(D)
	TLNE B,200000
	JRST THRD2	;PREV DEFINED
	PUSHJ P,DOWN	;ENTER LINK REQUEST
	MOVEM T,(D)
	JRST DATABK

THRD2:	HRRZ B,T
	MOVE T,1(D)
	PUSHJ P,UNTHR
	JRST DATABK

LOCGLO:	JUMPGE D,DATABK	;LOCAL-GLOBAL RECOVERY
	MOVE T,D
	TLO A,40000	;GLOBAL
	PUSHJ P,LKUP1B	;FIND OCCURANCE OF GLOBAL
	IORM A,(T)	;SMASH OLD LOCAL OCCURENCE
	JUMPGE D,DATABK
	TLNN B,200000
	JRST DATABK
	MOVE B,1(D)	;ALREADY DEFINED
	MOVEM B,T1
	HRRZM D,T2
	ADDI D,2
	PUSHJ P,PATCH	;CLOBBER DEFINITION
	MOVE D,BOT
	PUSH P,CDATABK
	JRST PATCH7	;FILL IN OLD LOCAL REQ

LIBREQ:	JUMPL D,DATABK	;ALREADY THERE
	MOVEI T,0
	JRST USE1

REPT:	MOVEM T,TIMES
	JRST DATABK

COMMON:	ADD RH,COMLOC
	JRST COM1

DEFPT:	MOVEI T,@LKUP3
	TRO FF,GPARAM
	JRST DFSYM1



LDCND:	TRO FF,COND
	JRST LIB

LIB6:	CAIN A,12	;END OF CONDITIONAL
	JRST .OMIT1
	HRRZS T
	CAIN A,1
	CAIE T,5	;LOADER VALUE CONDITIONAL
	CAIN A,11	;COUNT MATCHING CONDITIONALS
	AOS FLSH
	JRST OMIT

LIB2:	TRNE FF,COND
	JRST LIB6
	CAIN A,5
	JRST LIB7
	PUSHJ P,RPB
	CAIN A,4	;PRGM NAME
	TLNN T,40000	;REAL END
	JRST OMIT	;USED FOR "OMIT"
	JRST OMIT1	;LEAVE LIB SEARCH MODE

LIB1:	TRO FF,SEARCH
	PUSHJ P,RPB
	JUMPGE T,.-1
	TRZ FF,SEARCH
LIB4:	PUSHJ P,LKUP
	JUMPGE D,LIB3	;NOT ENTERED
	TRNE FF,COND
	JRST LIB5
	TLNE B,200000	;RQST NOT FILLED
LIB3:	TLC T,200000	;"AND NOT" BIT
LIB5:	TLNE T,200000
	JRST LIB1	;THIS ONE LOSES
LIB:	CLEARM FLSH
LIB7:	PUSHJ P,RPB
	JUMPGE T,LIB4
.OMIT1:	SOSGE FLSH
OMIT1:	TRZ FF,SEARCH+COND;END OF SEGMENT,LOAD THIS PROG
OMIT:	PUSH P,.


RPB:	SOSL TC
	JRST GTWD
	PUSHJ P,GTWD	;SOAK UP CKSUM
	AOJN CKS,RCKS

LOAD:	JRST (LL)	;READ SWITCH
LOAD2:	PUSHJ P,GTWD
	LDB A,[(220700)T]
	MOVEM A,TC
	MOVSI A,770000
	ANDCAM A,BITPTR
	LDB A,[(310700)T]
LOAD1:	MOVE P,SAVPDL
	JUMPLE T,OUT
	CAILE A,LOADTE-LOADTB
	JRST TPOK
	TRNE FF,SEARCH
	JRST LIB2
	JRST @.+1(A)
LOADTB:	TPOK
	LDCMD	;LOADER COMMAND (1)
	ABS	;ABSOLUTE (2)
	REL	;RELOCATABLE (3)
	PRGN	;PROGRAM NAME (4)
	LIB	;LIBRARY (5)
	COMLOD	;COMMON LOADING (6)
	GPA	;GLOBAL PARAMETER ASSIGNMENT (7)
SYMSW:	DDSYMS	;LOCAL SYMBOLS (10)
	LDCND	;LOAD TIME CONDITIONAL (11)
SYMFLG:	SETZ OMIT	;END LDCND (12)
LOADTE:
GPA:	PUSHJ P,RPB
	MOVEM T,T2
	MOVEI T,0

LDCMD:	ADDI T,LDCMD2+1
	HRRM T,LDCMD2
	ROT T,4
	DPB T,[(330300)LDCVAL]
	TRO FF,UNDEF+CODEF
	HRRM ADR,ADRM
	MOVEI B,@LKUP3
	MOVEM B,CPOINT+1
	MOVEI ADR,T1
	JSP LL,DATABK

LDCMD1:	TRZ FF,UNDEF+CODEF
	HRRZ ADR,ADRM
	CLEARB RH,AWORD
	MOVE D,T1
LDCMD2:	JRST @.
	GPA1
	JMP	;JUMP BLOCK (1)
	GLOBAL	;GLOBAL LOCATION ASSIGNMENT (2)
	COMSET	;COMMON ORIGIN (3)
	RESPNT	;RESET GLOBAL RELOCATION (4)
	LDCVAL	;LOADER VALUE CONDITIONAL (5)
	.OFFSET	;GLOBAL OFFSET (6)
	L.OP	;LOADER EXECUTE (7)


JMP:	JUMPE D,JMP1
	MOVEM D,SA
JMP1:	MOVEI LL,LOAD2
	JRST LOAD2

GLOBAL:	TRO FF,INDEF
	HRRM D,RELADR
	MOVE ADR,D
	MOVEI D,RELADR
GLOB1:	HRRM D,REL
	JRST JMP1

RESPNT:	TRZ FF,INDEF
	MOVEI D,FACTOR
	JRST GLOB1

LDCVAL:	JUMP D,JMP1
	TRO FF,SEARCH+COND
	CLEARM FLSH
	JRST JMP1

.OFFSET:	HRRM D,LKUP3
	JRST JMP1

L.OP:	MOVE B,T1	;B=3 C=4 D=5
	TDNN B,[(757)777777]
	IOR B,[0 4,5]
	MOVE 4,T1+1
	MOVE 5,T1+2
	XCT B
	MOVEM 4,.VAL1
	MOVEM 5,.VAL2
	JRST JMP1



GPA1:	MOVE T,T2
	PUSHJ P,LKUP
	MOVE T,T1
	MOVEI TT,100	;DON'T GENERATE MDG
	TRO FF,GPARAM
	PUSHJ P,DEFSYM
	JRST JMP1

DDSYMS:	MOVN D,TC
	HRL C,BOT
	ADDM D,BOT
	HRR C,BOT	;(OLD BOT)NEW BOT
	ADDM D,DDPTR
	HRLZS D
	ADDB D,DDPTR
	BLT C,-1(D)
C8:	SOJ D,8.

DDLUP:	PUSHJ P,RPB
	LDB TT,[(410300)T]
	TLZ T,300000
	TLO T,100000
	PUSH D,T
	PUSHJ P,RRELOC
	PUSH D,T
	JRST DDLUP


PRGN:	PUSHJ P,RPB
	MOVE A,T
	MOVE T,FACTOR
	HRL T,ADR
	TLNE A,40000	;REAL PRGM END
	HRRZM ADR,FACTOR
	TLO A,740000
	PUSHJ P,ENT
	MOVE B,DDPTR
	MOVEI D,-2(BOT)
	HRL D,BOT
	HRR BOT,D
	BLT D,-3(B)
	HRRZ D,BOT
	TLZ A,140000
	MOVEM A,-2(B)
	MOVEM T,-1(B)
	PUSHJ P,SYMS
	MOVSI C,740000
	SKIPL SYMSW
	ANDCAM C,(B)
	HLLZS LKUP3
	PUSHJ P,RESET
	JRST OMIT

GTWD:	PUSHJ P,ARPB
	JFCL 4,.+1
	ADD CKS,T
	JFCL 4,[AOJA CKS,.+1]
RELADR:	POPJ P,


ARPB:	MOVEI T,200000
	CONSO PTR,10
	SOJGE T,.-1
	JUMPL T,CPOPJ
	DATAI PTR,T
ADRM:	POPJ P,

GETBIT:	ILDB TT,BITPTR
	SKIPL BITPTR
	POPJ P,
	EXCH T,BITS
	SOS BITPTR
	PUSHJ P,RPB
	EXCH T,BITS
	LDB TT,BITPTR
CPOPJ:	POPJ P,


AWORD:	0
HIGHCD:	0
SA:	0
TC:	0
BITS:	0
BITPTR:	(300)BITS
SAVPDL:	0
LBOT:	DDPTR
TIMES:	0
COMLOC:	ICOMM
T1:	0
T2:	0
FLSH:	0

EISYM:	;INITIAL SYMBOLS

CRELPT:	SQUOZE 64,$R.
FACTOR:	100
CPOINT:	SQUOZE 64,$.
	0
	SQUOZE 64,.LVAL1
.VAL1:	0
	SQUOZE 64,.LVAL2
.VAL2:	0
EISYME:

RCKS:	(7000+SIXBIT /CKS/)
	JRST LI3

;LOADER INTERFACE

TYPR:	0
	PUSH P,C
	PUSH P,T
	PUSH P,TT
	LDB C,[(330300)40]
	ORCMI C,7
	MOVE TT,[(220600)40]
TYPR2:	PUSHJ P,SIXTYP
	AOJE C,TYPR1
	PUSHJ P,SPC
	HRRZ T,ADR
	PUSHJ P,OPT
	AOJE C,TYPR1
	PUSHJ P,SPC
	PUSHJ P,ASPT
	AOJN C,[JRST 4,@.]
TYPR1:	PUSHJ P,CRL
	POP P,TT
	POP P,T
	POP P,C
	JRST 2,@TYPR

ASPT:	MOVE T,A
SPT:	TLNN T,40000
	TYPE "*
SPT2:	TLZ T,740000
SPT1:	IDIVI T,50
	HRLM TT,(P)
	JUMPE T,.+2
	PUSHJ P,SPT1
	HLRZ T,(P)
	ADDI T,260-1
	CAILE T,271
	ADDI T,301-272
	CAILE T,332
	SUBI T,334-244
	CAIN T,243
	MOVEI T,256
	CAIN T,257
SPC:	MOVEI T,40
	JRST TYO



OPTCR:	PUSH P,CCRL
OPT:	SKIPA TT,C8
DPT:	MOVEI TT,10.
	HRRM TT,OPT1
OPT2:	LSHC T,-43
	LSH TT,-1
OPT1:	DIVI T,10
	HRLM TT,(P)
	JUMPE T,.+2
	PUSHJ P,OPT2
	HLRZ T,(P)
	ADDI T,260
	JRST TYO

TAB:	PUSHJ P,SPC
	PUSHJ P,SPC
	JRST SPC

CRL:	TYPE 15
CRT:	TYPE 12
	POPJ P,

TYI:	MOVEI T,12
	TRZE FF,LINF
	JRST TYO2
	CONSO TTY,40
	JRST .-1
	DATAI TTY,T
TYO:	ANDI T,177
	CAIE T,176
	CAIN T,175
	JRST TYO3
TYO2:	CONSZ TTY,20
	JRST .-1
	DATAO TTY,T
	CONSZ TTY,40
	DATAI TTY,T
	CAIE T,377
	POPJ P,
	JRST LI3

TYO3:	TYPE "$
	POPJ P,

TYP:	0
	PUSH P,T
	MOVE T,TYP
	LDB T,[270600+T,,-1]
	PUSHJ P,TYO
	POP P,T
	JRST 2,@TYP
LI4:	CAMN A,[(10700)CBUF-1]
	JRST LI3
	LDB T,A
	ADD A,[(70000)]
	SKIPGE A
	SUB A,[(430000)1]
	PUSHJ P,TYO
	JRST LI1

LIS:	CONO 635550
	MOVSI FF,LSTP
	CONO DC,0
	CONO 3000+APRCHN
	CONO PI,12260
LI3:	MOVE A,[(10700)CBUF-1]
	MOVEM A,CPTR
	MOVE P,[(,-LPDL)PDL-1]
	PUSHJ P,CRL
	TRZ FF,LINF
LI1:	TRZ FF,ALTF
LI2:	PUSHJ P,TYI
	CAIN T,177	;RUBOUT
	JRST LI4
	IDPB T,A
	CAMN A,[(10700)MACDMP-200-2-1]
	JRST LI4


LIS1:	CAIN T,15
	TROA FF,LINF
	CAIE T,175
	JRST LI1
	TRON FF,ALTF
	JRST LI2
	PUSHJ P,CRL
CD:	MOVEI D,0
CD3:	TRZ FF,ARG
CD2:	ILDB T,CPTR
	CAIL T,"0
	CAILE T,"9
	JRST CD1
	LSH D,3
	ADDI D,-"0(T)
VALRET:	TRO FF,ARG
	JRST CD2

CD1:	CAIN T,07
	JRST MCDMP
	CAIN T,175
	JRST LI3
	CAIL T,"<
	CAILE T,"[
	JRST CD
	IDIVI T,4
	LDB T,DTAB(TT)
	PUSHJ P,SLIS(T)
	JRST CD
	JRST VALRET


SLIS:	TDZA C,C
MLIS:	MOVEI C,2
	TRNE FF,ARG
	JUMPL D,LIST
	MOVE D,LBOT
	JRST LIST

CCRL:	AOBJP D,CRL	;USED FOR CRL
CCRL1:	AOBJP D,CRL
LIST:	SKIPL A,(D)
	JRST .-2
	LDB TT,[(410300)A]
	ORCMI TT,7
	AOJN TT,LIST2	;NOT PRGM NAME
LIST4:	PUSHJ P,ASPT
LIST5:	PUSHJ P,TAB
	HRRZ T,1(D)
	TRNN FF,ARG
	SKIPN C
	MOVE T,1(D)
	PUSHJ P,OPTCR
	TRNN FF,ARG
	JRST CCRL
	AOBJP D,CRL
	SKIPGE 1(D)
	JRST CCRL1
	JRST LIST5

LIST2:	XOR TT,C		;MISSING OR DEFINED
	AOJN TT,CCRL
	PUSHJ P,SPC
	JRST LIST4



TDDT:	MOVE B,DDPTR
	HRRZ D,LBOT
	MOVE BOT,D
	TRO FF,GLOSYM

SYMS:	CAIN D,(B)
	JRST SYMS5
	SKIPL A,(D)
SYMS6:	AOJA D,SYMS
	TLNN A,200000	;DEFINED
	AOJA D,SYMS6
	TLNE A,40000	;LOCAL
	TRNE FF,GLOSYM	;OK FOR GLOBALS
SYMS2:	TLNE A,100000	;FLUSH PRGM NAME
SYMS1:	AOJA D,SYMS6
	TRNN FF,GLOSYM
	SKIPL SYMSW
	JRST SYMS3	
	HRRZM D,T2	;OMIT SYMBOLS
	ADDI D,2
	PUSHJ P,PATCH	;DELETE FROM TABLE
	JRST SYMS

SYMS3:	TLZ A,740000
	MOVE T,1(D)
	MOVSI C,2(D)
	HRR C,D
	BLT C,-3(B)	;REMOVE DEF IN LOADER TABLE
	SUB B,[(2)2]
	TLO A,40000
	TRNN FF,GLOSYM
	TLC A,140000	;DDT LOCAL
	MOVEM A,(B)
	MOVEM T,1(B)	;STORE VALUE IN NEW SLOT
	JRST SYMS

SYMS5:	MOVEM B,DDPTR
	HRRZ C,BOT
	SUB C,D
	HRLM C,BOT
SYMS7:	MOVEM BOT,LBOT
	POPJ P,

GO:	TRNE FF,ARG
	MOVEM D,SA
	PUSHJ P,LEAVE
	JRST @SA

EQLS:	SKIPA T,D
EXAM:	MOVE T,(D)
	JRST OPTCR

ZERO:	HRRZ D,LBOT	;BLOCK TRANSFER TEST
	CLEARM LOW
	MOVE A,[(LOW)LOW+1]
	BLT A,-1(D)
	MOVE A,[(C0)DISPL+LOWLOD]
	BLT A,DISPL+LOW-1
	CLEARM HIGHCD
C.CD2:	POPJ P,CD2

PAPER:	CONO PTR,60
	MOVEI A,ARPB
	JRST SETIN

GETCOM:	MOVE A,[10700,,CBUF-1]
	MOVEM A,CPTR
	MOVEI P,PDL-1
	PUSH P,C.CD2
	MOVEM P,SAVPDL
GTCM1:	PUSHJ P,RED
	TRNE T,1
	POPJ P,
	MOVEM T,1(A)
	CAME A,[10700,,MACDMP-200-2-1]
	AOJA A,GTCM1
TPOK:	SKIPA T,BELL
ERR:	MOVE T,"?
	PUSHJ P,TYO
	JRST LI3
UTAP:	TRZE FF,ARG
	PUSHJ P,OPNTP
	PUSHJ P,FRD
	PUSHJ P,LOOK
	JRST FNF
	MOVEM A,FILNO
	MOVE B,[(500)MOVTAB+2*23.-1]
	MOVEM B,RBYTP
	CLEARM RBYTS
	MOVE A,[RBEGB-1,,RBEGB-1]
	MOVEM A,RPNTR
	TLZ FF,EOF
	CLEARM RCNTR
	MOVEI A,RED
SETIN:	HRRM A,GTWD
CRET:	POPJ P,

FNF:	(7000+SIXBIT /FNF/)
	JRST LI3


LISTF:	TRZE FF,ARG
	PUSHJ P,OPNTP
	MOVEI B,23.
	MOVEI D,MOVTAB-2
	ADDI D,2
	SKIPN C,(D)
LISTF3:	SOJG B,.-2
	JUMPLE B,CPOPJ
	TLNN C,770000
	JRST LISTF3
	HRLOI TT,577+D
LISTF1:	PUSHJ P,SIXTYP
	PUSHJ P,SPC
	TRNN TT,1
	JRST LISTF1
	PUSHJ P,CRL
	JRST LISTF3

SIXTYP:	ILDB T,TT
	ADDI T,40
	TLNE TT,770000
	SOS (P)
	JRST TYO

LOADN:	SKIPA C,SYMFLG
LOADG:	MOVEI C,DDSYMS
	MOVEM C,SYMSW

RESTAR:	MOVE BOT,LBOT
	MOVEM P,SAVPDL
	CLEARB CKS,TC
	CLEARB RH,AWORD
	PUSH P,CJMP1
RESET:	MOVEI A,FACTOR	;LEAVE GLOBAL LOCATION MODE
	HRRM A,REL
	TRZA FF,UNDEF+GPARAM+INDEF+GLOSYM+SEARCH+CODEF+COND
SFACT:	MOVEM D,FACTOR
CJMP1:	POPJ P,JMP1

KILL:	MOVEI BOT,DDPTR
	MOVEM BOT,DDPTR
	JRST SYMS7

COMVAL:	SKIPA D,COMLOC
SADR:	HRRZ D,SA
POPJ1:	AOSA (P)
COMSET:	MOVEM D,COMLOC
BELL:	POPJ P,7

LBRAK:	MOVEM D,T1
	PUSHJ P,ISYM
	PUSHJ P,LKUP
	MOVE T,T1
	TRO FF,GPARAM
	TRZE FF,ARG
	JRST DFSYM2
	JRST POPJ1

SOFSET:	HRRM D,LKUP3
	POPJ P,

BEG:	SKIPA D,FACTOR
ENDD:	HRRZ D,LBOT
	JRST POPJ1

DDT:	MOVE ADR,HIGHCD
	MOVE A,DDPTR
	SUB A,[(NUMSYM)NUMSYM]
	SUBI ADR,(A)
	AOJLE ADR,DDT1
	(6000+SIXBIT /TMS/)
	AOJ ADR,
	TRZ ADR,1
	HRLS ADR
	ADD A,ADR
DDT1:	EXCH A,DDPTR
	HRL A,DDPTR
	MOVSS A
	BLT A,DDPTR-NUMSYM-1
	CLEARM DATABK
	PUSHJ P,LEAVE
	MOVE 16,[(DATABK)DATABK+1]
	JRST DD1



DEFINE FOUR A,B,C,D
	(<<A-SLIS>_9>+B-SLIS)<<C-SLIS>_9>+D-SLIS
	TERMIN

DTAB:	(331100+T)DTB-74/4
	(221100+T)DTB-74/4
	(111100+T)DTB-74/4
	(1100+T)DTB-74/4

DTB:	FOUR LBRAK,EQLS,ERR,MLIS,;< = > ?
	FOUR GETCOM,ERR,BEG,COMSET,	;@ A B C
	FOUR DDT,ENDD,LISTF,GO,	;D E F G
	FOUR ERR,ERR,ERR,KILL,	;H I J K
	FOUR LOADG,UTAP,LOADN,SOFSET,;L M N O
	FOUR PAPER,COMVAL,SFACT,SLIS,;P Q R S
	FOUR TDDT,ERR,ERR,ERR,	;T U V W
	FOUR SADR,ERR,ZERO,EXAM,;X Y Z [

IFLE 1000-DDT+SLIS,[PRINTX /DISPATCH OVERFLOW
/]
INFORM [DISPATCH ROOM]\<1000-DDT+SLIS>


EXCHN:	0
	CONO PI,11377
	MOVEI 1,LOW-LOWLOD-1
	MOVE LOWLOD(1)
	EXCH DISPL+LOWLOD(1)
	MOVEM LOWLOD(1)
	SOJGE 1,.-3
	JRST 2,@EXCHN


MCDMP:	PUSH P,CMCDMP
LEAVE:	TLNN FF,LSTP
	JRST .-1
	POP P,EXCHN
	JRST EXCHN+1

ISYM:	MOVSI C,(50*50*50*50*50*50)
	MOVSI T,40000	;GLOBAL BIT

ISYM0:	ILDB A,CPTR
	CAIN A,"*
	TLZ T,40000	;LOCAL
	CAIN A,"*
	JRST ISYM0
	CAIN A,">
	JRST LKUP
	SUBI A,"0-1
	CAIL A,"A-"0+1
	SUBI A,"A-"0+1-13
	JUMPGE A,ISYM2
	ADDI A,61
	CAIN A,60
	MOVEI A,45	;.
ISYM2:	IDIVI C,50
	IMUL A,C
	ADDM A,T
	JRST ISYM0

APRBRK:	0
	CONSO UTC,700
	TLNE FF,SDLY
	JRST ILMEM
	PUSH P,T
	MOVE T,RCNTR
	CAIG T,NURBUF*200-200
	PUSHJ P,ALOAD
	CONSO UTC,700
	SOSE URUNTM
	JRST APRET
	CONO UTC,5000+PCHCHN
	TLO FF,SDLY
APRET:	POP P,T
ILMEM:	CONSZ 10000
	(7000+SIXBIT /ILM/)
	CONO 431440+APRCHN
	JRST 12,@APRBRK


PITAPE:	0
UTP1:	DATAI DC,.
UTP2:	AOS .-1
	SOSLE PICNT
	JRST 12,@PITAPE
	CONO PI,1000+TPCHNA
	CONO DC,TPCHN
	JRST 12,@PITAPE

PIPOS:	CONSZ UTS,16
	JRST UTAPER
	MOVEM A,ATEM
	MOVEM B,BTEM
UTAPE:	MOVEI A,20	;THIS GETS PRGM MOD.
	DATAI DC,B
UTBLK:	SUBI B,.
	JUMPE B,PIPOS2
UTAPE1:	TLNN FF,FORW
	SOSA B
	AOS B
	MOVEM B,UTENB
	ADDI B,3
	JUMPL B,.+3
	CAIG B,6
	JRST UTAPE4
	TLZN FF,FORW
	TRO A,2000
	CAILE B,2
	TRCA A,12000
	TLO FF,FORW
UTAPE3:	CONO UTC,320200+PCHCHN(A)
UTAPER:	CONO DC,4010+TPCHN
	JRST PI3


UTAPE2:	TRO A,3000
UTAPE4:	TLNN FF,FORW
	TRO A,10000
	JRST UTAPE3

PIPOS2:	SKIPE UTENB
	JRST UTAPE2
	MOVE B,PIREG
	TLNN FF,FORW
	TROA A,10000
	SUBI B,177
	MOVEM B,UTP1
	CONO UTC,360300+PCHCHN(A)
	MOVSI A,(AOS)
	TLNN FF,FORW
	MOVSI A,(SOS)
	HLLM A,UTP2
PI3:	MOVE A,ATEM
	MOVE B,BTEM
	JRST 12,@PITAPE


PBRK:	0
	PUSH P,A
	CONSZ UTC,4000
	CONSO UTS,20
	JRST JDBRK
	TLO FF,LSTP
	TLZ FF,SDLY
JDBRK1:	CONO UTC,0
	JRST POPRET
JDBRK:	CONSO UTS,17
	JRST POPRET	;PI REQUEST STORED IN SYSTEM
	CONSO UTC,700
	JRST JDBRK1
	CONSO PI,TPCHNA
	CONSZ UTS,16
	JRST UTERR2
	MOVEI A,200
UTCNTR:	ADDM A,RCNTR
	MOVEI A,24.
	MOVEM A,URUNTM
	CONI UTC,A
	TRZ A,700
	CONO UTC,(A)
	CONO PI,4000+1_<7-APRCHN>
POPRET:	POP P,A
	JRST 12,@PBRK


UTERR2:	HRRZ A,UTAPE
	CONSZ UTS,2
	TLZN FF,FORW
	TLOA FF,FORW
	TRO A,10000
	CONO UTC,323200+PCHCHN(A)
	PUSHJ P,TSTART
	JRST POPRET

RED:	SKIPN RCNTR
	JRST RED3
RED2:	SOS RCNTR
	AOS T,RPNTR
	HRRZS T
	CAIL T,RBEGB+NURBUF*200
	MOVEI T,RBEGB
	HRRM T,RPNTR
	MOVE T,(T)
	POPJ P,

RED3:	TLNE FF,EOF
	SKIPE RCNTR
	JRST RED
OUT:	MOVE P,SAVPDL
CMCDMP:	POPJ P,MACDMP

;RDATAS:	(,RBEGB-1)RBEGB+<NURBUF-1>*200-1
RPNTR:	0
RCNTR:	NURBUF*200
RBYTS:	0
RBYTP:	(500)MOVTAB+2*23.-1


BYTDEC:	MOVSI D,50000
	ADD D,RBYTP
	SKIPGE D
	SUB D,[(430000)1]
	MOVEM D,RBYTP
	LDB T,D
	POPJ P,

ITAB:	AOS TT,RBYTS
	ILDB T,RBYTP
	SOS TT,RBYTS
	PUSHJ P,BYTDEC

ALOAD:	TLNE FF,EOF
	POPJ P,
	MOVEM LL,UAC+LL
	MOVE LL,[(2)UAC+2]
	BLT LL,UAC+LL-1
RLOAD:	CLEARB A,B
	SKIPGE RBYTS
	MOVEI A,2	;USES A,B,C,D,LL,TT,T
	MOVE C,FILNO
LUP1:	XCT ITAB(A)
	MOVE LL,RBYTP
	XCT ITAB+1(A)
	TRNE TT,-1
	CAIN T,37
	JRST LDRED	;END OF TAPE
	CAIE T,(C)
	JRST LUP1
;START UP TAPE ROUTINE
	HRRM TT,UTBLK
	HRRZ B,UTAPE
	TLZE FF,LSTP
	PUSHJ P,LOADST
OUT4:	HLRZ C,RPNTR
	ADDI C,200
	HRRM C,PIREG
	CAILE C,RBEGB+<NURBUF-1>*200-1
	MOVEI C,RBEGB-1
	HRLM C,RPNTR
	CONI UTC,B
	TRZN B,700
	CONO UTC,200(B)
	PUSHJ P,TSTART
OUT3:	MOVS LL,[(2)UAC+2]
	BLT LL,LL
	POPJ P,


LOADST:	TLO FF,FORW
	SKIPL RBYTS
	TLZA FF,FORW
	SUBI B,10000
	CONO UTC,333200+PCHCHN(B)
	POPJ P,

TSTART:	CONO DC,4010+TPCHN
	MOVE A,[JRST PIPOS]
	MOVEM A,UTP1
	MOVEI A,200
	MOVEM A,PICNT
	SETOM UTENB
	CONO PI,2200+TPCHNA
	POPJ P,
LDRED:	PUSHJ P,LOOK
	JRST LDRD2	;NO FILE EXTENTION
	MOVEM A,FILNO
	MOVSI B,400000
	XORM B,RBYTS
	JRST RLOAD
LDRD2:	MOVEM LL,RBYTP
	TLO FF,EOF
	JRST OUT3

LOOK:	HRLZI A,-23.
	MOVEI TT,MOVTAB-2
LOOK2:	ADDI TT,2
	CAMN B,(TT)
	CAME C,1(TT)
	AOBJN A,LOOK2
	TLZN A,-1
	POPJ P,	;NOT FOUND
	AOJA A,POPJ1

FRD2:	SKIPA B,C
FRD:	MOVSI B,400000
	MOVSI C,400000
	MOVE A,[(600)C-1]
FRD1:	ILDB T,CPTR
	CAIN T,175
	POPJ P,
	TRC T,40
	JUMPE T,FRD2
	CAIN T,77
	MOVEI T,0
	CAME A,[(600)C]
	IDPB T,A
	JRST FRD1



OPNTP:	TLNN FF,LSTP
	JRST .-1
	TLO FF,FORW
	LSH D,3
	ANDI D,70
	HRRM D,UTAPE
	MOVEI B,MOVTAB+177
	HRRM B,PIREG
	MOVEM B,RCNTR
	MOVEI A,100
	HRRM A,UTBLK
	CONO UTC,322200+PCHCHN(D)
	TLZ FF,LSTP
	PUSHJ P,TSTART
	CONSO UTC,700
	POPJ P,
	JRST .-2

CPTR:	0
PICNT:	0
ATEM:	0
BTEM:	0
URUNTM:	0
UTIMSV:	0
PIREG:	DATAI DC,
FILNO:	0
UTENB:	-1
CONSTANTS

C0:	0
DISPL=.-LOWLOD


LOC 40+DISPL+APRCHN*2
JSR APRBRK


LOC 40+DISPL+PCHCHN*2
JSR PBRK

LOC 40+DISPL+TPCHN*2
JSR PITAPE

LOC DISPL+41
	JSR TYPR
LOC DISPL+LOW


UAC=.-2
	BLOCK LL+1-2

MOVTAB:	ZZZ=56
	IRP A,,[NO,FILE,DIREC,TORY]
	SIXBIT /A/
	ZZZ=ZZZ-1
	TERMIN

	REPEAT ZZZ,0
	757367573674
	REPEAT 10,0
	(660000)0
	REPEAT 107,0
	-1

PDL:	CONO 635550
	MOVEI A,100
	MOVEM A,FACTOR
	MOVEI A,ICOMM
	MOVEM A,COMLOC
	MOVEI A,
	HRRM A,LKUP3
	MOVEI A,FACTOR
	HRRM A,REL
	MOVEI P,PDL
	PUSHJ P,KILL
	MOVE A,[(DTEXT1)COMPTR-LTEXT-1]
	BLT A,COMPTR-2
	MOVE A,[(700)COMPTR-LTEXT-2]
	MOVEM A,COMPTR
	JSR EXCHN
	JRST LIS

DTEXT1:	ASCII /M}OPDDTG}/	;M$OPDDT G$
LTEXT=.-DTEXT1

CONSTANTS

LOC PDL+LPDL
RBEGB:
	BLOCK NURBUF*200
CBUF:
INFORM [HIGHEST USED]\<CBUF>
INFORM [COMMAND BUFFER LENGTH]\<<37176+MOBY-CBUF>*5>
INFORM [LOADER LENGTH]\<TYPR-DDPTR>

END PDL


